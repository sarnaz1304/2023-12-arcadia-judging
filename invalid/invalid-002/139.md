Salty Lead Fox

high

# Happy flow rewards are not backed by account's assets, leading to bad debt

## Summary
The happy flow does not use accounts' assets to pay out `initiationReward`, `liquidationPenalty`, and `terminationReward`. This will lead to bad debt as the rewards are not backed by anything.

## Vulnerability Detail
Liquidations can be started by anyone when they see that an account has become liquidatable. This is done through `Liquidator::liquidateAccount()`. Once an auction has been initiated, anyone can start bidding for the account's assets. The auction uses an algorithm that decreases the price of the auctioned assets as time passes. Liquidations can be settled either through the happy or unhappy flow. We can look at the following piece of code from `Liquidator::bid()`:

```solidity
function bid(address account, uint256[] memory askedAssetAmounts, bool endAuction_) external nonReentrant {
    AuctionInformation storage auctionInformation_ = auctionInformation[account];
    if (!auctionInformation_.inAuction) revert LiquidatorErrors.NotForSale();

    // Calculate the current auction price of the assets being bought.
    uint256 totalShare = _calculateTotalShare(auctionInformation_, askedAssetAmounts);
    uint256 price = _calculateBidPrice(auctionInformation_, totalShare);

    // Transfer an amount of "price" in "Numeraire" to the LendingPool to repay the Accounts debt.
    // The LendingPool will call a "transferFrom" from the bidder to the pool -> the bidder must approve the LendingPool.
    // If the amount transferred would exceed the debt, the surplus is paid out to the Account Owner and earlyTerminate is True.
    uint128 startDebt = auctionInformation_.startDebt;
    bool earlyTerminate = ILendingPool(auctionInformation_.creditor).auctionRepay(
        startDebt, auctionInformation_.minimumMargin, price, account, msg.sender
    );

    // Transfer the assets to the bidder.
    IAccount(account).auctionBid(
        auctionInformation_.assetAddresses, auctionInformation_.assetIds, askedAssetAmounts, msg.sender
    );

    // ...
}
```

Here's the code invoked inside `AccountV1`:

```solidity
function auctionBid(
    address[] memory assetAddresses,
    uint256[] memory assetIds,
    uint256[] memory assetAmounts,
    address bidder
  ) external onlyLiquidator nonReentrant {
      _withdraw(assetAddresses, assetIds, assetAmounts, bidder);
}

function _withdraw(
    address[] memory assetAddresses,
    uint256[] memory assetIds,
    uint256[] memory assetAmounts,
    address to
) internal {
    // If a Creditor is set, batchProcessWithdrawal will also update the exposures of assets and underlying assets for the Creditor.
    uint256[] memory assetTypes =
        IRegistry(registry).batchProcessWithdrawal(creditor, assetAddresses, assetIds, assetAmounts);

    for (uint256 i; i < assetAddresses.length; ++i) {
        // Skip if amount is 0 to prevent transferring 0 balances.
        if (assetAmounts[i] == 0) continue;

        if (assetTypes[i] == 0) {
            if (assetIds[i] != 0) revert AccountErrors.InvalidERC20Id();
            _withdrawERC20(to, assetAddresses[i], assetAmounts[i]);
        } else if (assetTypes[i] == 1) {
            if (assetAmounts[i] != 1) revert AccountErrors.InvalidERC721Amount();
            _withdrawERC721(to, assetAddresses[i], assetIds[i]);
        } else if (assetTypes[i] == 2) {
            _withdrawERC1155(to, assetAddresses[i], assetIds[i], assetAmounts[i]);
        } else {
            revert AccountErrors.UnknownAssetType();
        }
    }
}
```

This sends the bidder the asked assets. If the bidder bids for all of it and repays the debt owed by the account, the happy flow should be triggered:

```solidity
function _settleLiquidationHappyFlow(
    address account,
    uint256 startDebt,
    uint256 minimumMargin_,
    address terminator,
    uint256 surplus
) internal {
    (uint256 initiationReward, uint256 terminationReward, uint256 liquidationPenalty) =
        _calculateRewards(startDebt, minimumMargin_);

    // Pay out the "liquidationPenalty" to the most Junior Tranche and Treasury.
    _syncLiquidationFee(liquidationPenalty);

    totalRealisedLiquidity =
        SafeCastLib.safeCastTo128(totalRealisedLiquidity + terminationReward + liquidationPenalty + surplus);

    unchecked {
        // Pay out any surplus to the current Account Owner.
        if (surplus > 0) realisedLiquidityOf[IAccount(account).owner()] += surplus;
        // Pay out the "terminationReward" to the "terminator".
        realisedLiquidityOf[terminator] += terminationReward;
    }
}
```

Looking at it, we can see that there is a reward distribution. `liquidationPenalty` goes to the liquidity providers, `terminationReward` goes to the last bidder (who also ends the auction), and `initiationReward` was already paid when the liquidation had started.

But all of the account's assets had already been distributed. The rewards given here are not backed by the account's assets, they're just being calculated off the starting debt of it.

## Impact
Bad debt for the lending pool as rewards given in the happy flow are not backed by accounts' assets.

## Code Snippet
https://github.com/sherlock-audit/2023-12-arcadia/blob/main/lending-v2/src/Liquidator.sol#L199-L241
https://github.com/sherlock-audit/2023-12-arcadia/blob/main/lending-v2/src/Liquidator.sol#L286-L304
https://github.com/sherlock-audit/2023-12-arcadia/blob/main/accounts-v2/src/accounts/AccountV1.sol#L555-L562
https://github.com/sherlock-audit/2023-12-arcadia/blob/main/lending-v2/src/LendingPool.sol#L938-L959

## Tool used

Manual Review

## Recommendation
Account for the rewards from the account's assets. Since they're calculated based on the starting debt, the solution would be to just take them when the liquidation starts. Then bidders would be bidding for the remaining assets of the account. When the liquidation ends, distribute the rewards based on the current logic of the happy flow (no changes here).