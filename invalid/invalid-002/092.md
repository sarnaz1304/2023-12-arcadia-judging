Docile Mahogany Spider

high

# The getRateInUsd function may return an incorrect price.

## Summary
We assess the `asset price` using the `getRateInUsd` function in the `Registry`. 
However, it's important to note that this function has the potential to return `incorrect prices`.
This poses a significant risk.
## Vulnerability Detail
If we intend to utilize certain `assets`, we have to add them into the `asset module`. 
Let's consider an asset named `X`.
Each `asset` should have an associated `oracle sequence` for pricing.

Let's suppose there are two `oracles` in the sequence for asset `X`.
The first `oracle` is `("X", "Y")`, where the base asset is `X`, and the second is `("USD", "Y")`, where the base asset is `Y`.
Meaning, the first `oracle` provides the price of `X` in units of `Y`, while the second `oracle` provides the price of `Y` in units of `USD`.
```solidity
bool[] memory boolValues = new bool[](2);
uint80[] memory uintValues = new uint80[](2);
uintValues[0] = chainlinkOM.addOracle(oracleETH, "X", "Y", 2 days);
uintValues[1] = chainlinkOM.addOracle(oracleETH, "USD", "Y", 2 days);
boolValues[0] = true
boolValues[1] = false 
bytes32 oracleSequence = BitPackingLib.pack(boolValues, uintValues);
```

- To utilize `X`, it must be added to the `asset module`. 
  During this process, we verify the `oracleSequence` for this `asset`.
```solidity
function addAsset(address asset, bytes32 oracleSequence) external onlyOwner {
    if (!IRegistry(REGISTRY).checkOracleSequence(oracleSequence)) revert BadOracleSequence();
}
```
- It will certainly pass, as the `oracleSequence` is correct, as evidenced below.
```solidity
function checkOracleSequence(bytes32 oracleSequence) external view returns (bool) {
    (bool[] memory baseToQuoteAsset, uint256[] memory oracles) = oracleSequence.unpack();
    uint256 length = oracles.length;
    if (length == 0) revert RegistryErrors.Min1Oracle();
    // Length can be maximally 3, but no need to explicitly check it.
    // BitPackingLib.unpack() can maximally return arrays of length 3.

    address oracleModule;
    bytes16 baseAsset;
    bytes16 quoteAsset;
    bytes16 lastAsset;
    for (uint256 i; i < length; ++i) {
        oracleModule = oracleToOracleModule[oracles[i]];

        if (!IOracleModule(oracleModule).isActive(oracles[i])) return false;
        (baseAsset, quoteAsset) = IOracleModule(oracleModule).assetPair(oracles[i]);

        if (i == 0) {
            lastAsset = baseToQuoteAsset[i] ? quoteAsset : baseAsset;
        } else {
            // Last asset of an oracle must match with the first asset of the next oracle.
            if (baseToQuoteAsset[i]) {
                if (lastAsset != baseAsset) return false;
                lastAsset = quoteAsset;
            } else {
                if (lastAsset != quoteAsset) return false;
                lastAsset = baseAsset;
            }
        }
        // Last asset in the sequence must end with "USD".
        if (i == length - 1 && lastAsset != "USD") return false;
    }

    return true;
}
```
```solidity
i = 0 -> 
     baseToQuoteAsset[0] = true
     baseAsset : X, quoteAsset: Y
     lastAsset: Y
i = 1 ->
     baseToQuoteAsset[1] = false
     baseAsset: USD, quoteAsset: Y
     lastAsset = quoteAsset: true

     lastAsset: baseAsset

     lastAsset = USD: true
```
- When we need to determine the price of `X`, we utilize the `getRateInUsd` function.
```solidity
function getRateInUsd(bytes32 oracleSequence) external view returns (uint256 rate) {
    (bool[] memory baseToQuoteAsset, uint256[] memory oracles) = oracleSequence.unpack();

    rate = 1e18;

    uint256 length = oracles.length;
    for (uint256 i; i < length; ++i) {
        // Each Oracle has a fixed base asset and quote asset.
        // The oracle-rate expresses how much tokens of the quote asset (18 decimals precision) are required
        // to buy 1 token of the BaseAsset.
        if (baseToQuoteAsset[i]) {
            // "Normal direction" (how much of the QuoteAsset is required to buy 1 token of the BaseAsset).
            // -> Multiply with the oracle-rate.
            rate = rate.mulDivDown(IOracleModule(oracleToOracleModule[oracles[i]]).getRate(oracles[i]), 1e18);
        } else {
            // "Inverse direction" (how much of the BaseAsset is required to buy 1 token of the QuoteAsset).
            // -> Divide by the oracle-rate.
            rate = rate.mulDivDown(1e18, IOracleModule(oracleToOracleModule[oracles[i]]).getRate(oracles[i]));
        }
    }
}
```

If it requires `2` tokens of `Y` for `1` token of `X`, the first `oracle` returns `2 * 1e18`.
`IOracleModule(oracleToOracleModule[oracles[0]]).getRate(oracles[0]) = 2 * 1e18`
If it requires `2` tokens of `USD` for `1` token of `Y`, the second `oracle` returns `2 * 1e18`.
`IOracleModule(oracleToOracleModule[oracles[1]]).getRate(oracles[1]) = 2 * 1e18`

Then, indeed, we require `4` tokens of `USD` for `1` token of `X`.

However, the above function returns `1` token of `USD` for `1` token of `X`.
```solidity
rate = 1e18;
i = 0 ->
    baseToQuoteAsset[0] = true
    rate = rate * IOracleModule(oracleToOracleModule[oracles[0]]).getRate(oracles[0]) / 1e18 = 2 * 1e18.
i = 1 ->
    baseToQuoteAsset[0] = false
    rate = rate * 1e18 / IOracleModule(oracleToOracleModule[oracles[1]]).getRate(oracles[1]) = 2 * 1e18 * 1e18 / (2 * 1e18) = 1e18
```

## Impact
In summary, if there is a reversed `oracle` in the `oracleSequence`, this function returns a significantly incorrect price. 
This could lead to numerous dangerous `outcomes`.
Numerous users may fall victim to fund losses through `liquidation` resulting from incorrect pricing, while malicious users can exploit this situation for their gain.
## Code Snippet
https://github.com/sherlock-audit/2023-12-arcadia/blob/main/accounts-v2/src/asset-modules/ERC20-Primaries/ERC20PrimaryAM.sol#L48
https://github.com/sherlock-audit/2023-12-arcadia/blob/main/accounts-v2/src/Registry.sol#L296-L315
https://github.com/sherlock-audit/2023-12-arcadia/blob/main/accounts-v2/src/Registry.sol#L586-L598
## Tool used

Manual Review

## Recommendation
```solidity
function getRateInUsd(bytes32 oracleSequence) external view returns (uint256 rate) {
    (bool[] memory baseToQuoteAsset, uint256[] memory oracles) = oracleSequence.unpack();

    rate = 1e18;

    uint256 length = oracles.length;
    for (uint256 i; i < length; ++i) {
-         if (baseToQuoteAsset[i]) {
-             rate = rate.mulDivDown(IOracleModule(oracleToOracleModule[oracles[i]]).getRate(oracles[i]), 1e18);
-         } else {
-             rate = rate.mulDivDown(1e18, IOracleModule(oracleToOracleModule[oracles[i]]).getRate(oracles[i]));
-         }

+        rate = rate.mulDivDown(IOracleModule(oracleToOracleModule[oracles[i]]).getRate(oracles[i]), 1e18);
    }
}
```