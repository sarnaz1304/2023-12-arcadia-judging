Teeny Champagne Newt

medium

# Changing a creditor can disable Account features via grace period

## Summary

When an account owner changes creditors, it's possible for the oracle sequencer to be considered turned off. This will lead to various Account features from no longer working, including liquidations.

This bug hinges on the fact that each creditor may have a different grace period. If an account owner switches creditors such that the new creditor has a lower grace period and the oracle response is beyond that grace period, once the Account has switched creditors the Account's oracle will be turned off leading to various features no longer working. 

## Vulnerability Detail

A user whether by innocence or malicious intent can decide to switch creditor's at any time via calling openMarginAccount() with the new creditor. Each creditor has their own grace period, some longer, some shorter. If a creditor is switched over for an Account and the grace period is short enough that the oracle is considered down based on the logic within the Registry. sequencerNotDown modifier, several critical functions in the Registry contract will revert. Below is a code sample which shows the Registry contract logic and how the creditor's grace period determines whether or not the sequencer is down.

```solidity
modifier sequencerNotDown(address creditor) {
    (, bool sequencerDown) = _isSequencerDown(creditor);
    if (sequencerDown) revert RegistryErrors.SequencerDown();
    _;
}

function _isSequencerDown(address creditor) internal view returns (bool success, bool sequencerDown) {
    // This guarantees that no stale oracles are consumed when the sequencer is down,
    // and that Account owners have time (the grace period) to bring their Account back in a healthy state.
    try IChainLinkData(sequencerUptimeOracle).latestRoundData() returns (
        uint80, int256 answer, uint256 startedAt, uint256, uint80
    ) {
        success = true;
        // AUDIT: See here that we rely on the creditor's grace period to determine if the sequencer is down or not.
        if (answer == 1 || block.timestamp - startedAt < riskParams[creditor].gracePeriod) {
            sequencerDown = true;
        }
    } catch { }
}
```

The four functions that will revert once in this sequencer down state are the following:

- Registry.getValuesInUsd()
- Registry.getValuesInNumeraire
- Registry.getTotalValue()
- Registry.getCollateralValue()

Since these functions will revert, certain functionalities within the Account will also revert such as calling for liquidations.

Below is a forge test showing that once the creditor changes a creditor to a shorter grace period any attempt at a liquidation will lead to a revert:

```solidity
function testFuzz_LockedAccount_openMarginAccount_DifferentCreditor_DifferentGracePeriods(
  uint112 exposure,
  uint112 maxExposure,
  uint32 time
) public {
  exposure = uint112(bound(exposure, 0, type(uint112).max - 1));
  maxExposure = uint112(bound(maxExposure, exposure + 1, type(uint112).max));

  // Mock the sequencer call
  vm.startPrank(users.creatorAddress);
  // address uptimeOracle = address(0x7);

  // Need to revert the current sequencer so we can set it to what we want 
  vm.mockCall(
      address(sequencerUptimeOracle),
      abi.encodeWithSelector(IChainLinkData.latestRoundData.selector),
      abi.encode(uint80(0), int256(100), uint256(block.timestamp - 150), uint256(0), uint80(0))
  );
  vm.stopPrank();

  vm.startPrank(users.riskManager);
  registryExtension.setRiskParametersOfPrimaryAsset(
      address(creditorUsd), address(mockERC20.stable1), 0, maxExposure, 0, 0
  );
  registryExtension.setRiskParametersOfPrimaryAsset(
      address(creditorStable1), address(mockERC20.stable1), 0, maxExposure, 0, 0
  );

  // Set diff grace periods
  registryExtension.setRiskParameters(
      address(creditorUsd),            // creditor 
      uint128(0),                      // minUsdValue 
      uint64(100),                     // gracePeriod 
      uint64(100)                      // maxRecursiveCalls 
  );

  registryExtension.setRiskParameters(
      address(creditorStable1),        // creditor 
      uint128(0),                      // minUsdValue 
      uint64(300),                     // gracePeriod 
      uint64(100)                      // maxRecursiveCalls 
  );

  vm.stopPrank();

  vm.prank(users.accountOwner);

  proxyAccount.openMarginAccount(address(creditorUsd));
  depositTokenInAccount(proxyAccount, mockERC20.stable1, exposure);
  vm.startPrank(users.accountOwner);
  proxyAccount.openMarginAccount(address(creditorStable1));
  vm.stopPrank();

  // AUDIT: I am temporarily bypassing the Liquidator.liquidateAccount() feature since I had test issues directly calling liquidator. This is ok since the liquidateAccount() function doesn't do any checks (besides confirming the account exists and is currently not up for auction) before account.startLiquidation() is called.
  vm.startPrank(creditorStable1.liquidator());
  ILiquidator liq = ILiquidator(creditorStable1.liquidator());
  vm.expectRevert();
  // AUDITE: starting a liquidation will revert, temporarily DOSing the liquidation feature.
  proxyAccount.startLiquidation(address(address(0x19)));
}
```

## Impact

Account may fall into state that temporarily DOS's features such as liquidation. This can be done by accident or by malicious intent. For a malicious user, the liquidation can be stalled in hopes that the market recovers.

Because of how unique this scenario must be, I consider this a MEDIUM bug.

## Code Snippet

https://github.com/sherlock-audit/2023-12-arcadia/blob/main/accounts-v2/src/Registry.sol?plain=1#L131-L135

https://github.com/sherlock-audit/2023-12-arcadia/blob/main/accounts-v2/src/Registry.sol?plain=1#L167-L178

## Tool used

Manual Review

## Recommendation

When a new creditor is added to an Account, there should be a check that the new creditor's sequencer is up, otherwise, revert since switching creditor's will automatically lock some of the Account features.
