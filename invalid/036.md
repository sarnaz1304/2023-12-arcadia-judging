Powerful Myrtle Hawk

high

# `assetToInformation` is never initialized in `PrimaryAM`, causing `setOracles` always fail due to `Min1Oracle`

## Summary

In the `PrimaryAM` contract, the variable `assetToInformation` is defined to store the `assetInformation`. But currently in the contract, the `assetToInformation` is not initialized in the contract. Thus, when `setOracles` is called, empty bytes will be regarded as `oldOracles`, and is used in `IRegistry(REGISTRY).checkOracleSequence(oldOracles)`. Since `empty bytes` will surely revert due to `Min1Oracle`, `setOrcales` will always fail.

## Vulnerability Detail

In the `PrimaryAM` contract, the variable `assetToInformation` is defined to store the `assetInformation`. 
```solidity
    mapping(bytes32 assetKey => AssetInformation) public assetToInformation;
```

But there is only in `setOracles` that the `assetToInformation` is ever set.
```solidity
    function setOracles(address asset, uint256 assetId, bytes32 newOracles) external onlyOwner {
        bytes32 assetKey = _getKeyFromAsset(asset, assetId);

        // At least one of the old oracles must be inactive before a new sequence can be set.
        bytes32 oldOracles = assetToInformation[assetKey].oracleSequence;
        if (IRegistry(REGISTRY).checkOracleSequence(oldOracles)) revert OracleStillActive();

        // The new oracle sequence must be correct.
        if (!IRegistry(REGISTRY).checkOracleSequence(newOracles)) revert BadOracleSequence();

        assetToInformation[assetKey].oracleSequence = newOracles;
    }
```
So, in the statement `IRegistry(REGISTRY).checkOracleSequence(oldOracles)`, the `oldOracles` will be `assetToInformation[assetKey].oracleSequence` which are empty.

In the `Registry::checkOracleSequence`, empty bytes will revert due to `Min1Oracle`.

```solidity
    function checkOracleSequence(bytes32 oracleSequence) external view returns (bool) {
        (bool[] memory baseToQuoteAsset, uint256[] memory oracles) = oracleSequence.unpack();
        uint256 length = oracles.length;
        if (length == 0) revert RegistryErrors.Min1Oracle();
        ...
    }
```

**Unless a function is defined to purposely set the `setAssetInformation`,  the function call `setOrcales` will always fail. But there is no doc/comment specifying this.**

It should be noted that in the mock file `PrimaryAMMock`, a function `setAssetInformation` is added, and should be called before `setOracles`.
```solidity
    function setAssetInformation(address asset, uint256 assetId, uint64 assetUnit, bytes32 oracles) public {
        bytes32 assetKey = _getKeyFromAsset(asset, assetId);
        assetToInformation[assetKey].assetUnit = assetUnit;
        assetToInformation[assetKey].oracleSequence = oracles;
    }
```

For the same reason, the function `PrimaryAM::getValue` will. also fail.

But in real cases, a `setAssetInformation` should be defined for `PrimaryAM`, since the contract is expected to work properly when its all interfaces are implemented. Adding patches in the `test file` does not mean the contract itself is free of bugs. Anyone who implements `PrimaryAM` without realizing this issue will certainly suffer from DOS.

Here is the PoC. **Add it in SetOracles.fuzz.t.sol**
```solidity
    function testFuzz_fail_setOracles(
        address asset,
        uint96 assetId,
        uint256 lengthOld,
        bool[3] memory directionsOld,
        uint80[3] memory oraclesOld,
        uint256 lengthNew,
        bool[3] memory directionsNew,
        uint80[3] memory oraclesNew
    ) public {
        // Add the old oracles and set the oracle sequence for the asset.
        lengthOld = bound(lengthOld, 1, 3);
        addOracles(lengthOld, directionsOld, oraclesOld);
        
        // And one of the old oracles is not active anymore.
        lengthNew = bound(lengthNew, 1, 3);
        for (uint256 i; i < lengthNew; ++i) {
            vm.assume(oraclesOld[0] != oraclesNew[i]);
        }
        oracleModule.setIsActive(oraclesOld[0], false);

        // Add the new oracles.
        addOracles(lengthNew, directionsNew, oraclesNew);
        bytes32 oracleSequenceNew = getOracleSequence(lengthNew, directionsNew, oraclesNew);

        vm.prank(users.creatorAddress);
        vm.expectRevert();
        assetModule.setOracles(asset, assetId, oracleSequenceNew);

    } 
```
It can be seen that `empty bytes` are used, and the transaction is reverted.
![image](https://github.com/sherlock-audit/2023-12-arcadia-jes16jupyter/assets/153517808/d218cebb-0cce-4ca8-9598-e7104dc85c65)


## Impact
Empty bytes will be regarded as `oldOracles` and cause a revert due to `Min1Oracle`, thus `setOrcales` will always fail. Causing DOS and function failure for the contract.

## Code Snippet

[PrimaryAM::assetToInformation](https://github.com/sherlock-audit/2023-12-arcadia/blob/main/accounts-v2/src/asset-modules/abstracts/AbstractPrimaryAM.sol#L35)
```solidity
    mapping(bytes32 assetKey => AssetInformation) public assetToInformation;
```

[PrimaryAM::setOracles](https://github.com/sherlock-audit/2023-12-arcadia/blob/main/accounts-v2/src/asset-modules/abstracts/AbstractPrimaryAM.sol#L92-L103)
```solidity
    function setOracles(address asset, uint256 assetId, bytes32 newOracles) external onlyOwner {
        bytes32 assetKey = _getKeyFromAsset(asset, assetId);

        // At least one of the old oracles must be inactive before a new sequence can be set.
        bytes32 oldOracles = assetToInformation[assetKey].oracleSequence;
        if (IRegistry(REGISTRY).checkOracleSequence(oldOracles)) revert OracleStillActive();

        // The new oracle sequence must be correct.
        if (!IRegistry(REGISTRY).checkOracleSequence(newOracles)) revert BadOracleSequence();

        assetToInformation[assetKey].oracleSequence = newOracles;
    }
```
[Registry::checkOracleSequence](https://github.com/sherlock-audit/2023-12-arcadia/blob/main/accounts-v2/src/Registry.sol#L285C1-L288C61)
```solidity
    function checkOracleSequence(bytes32 oracleSequence) external view returns (bool) {
        (bool[] memory baseToQuoteAsset, uint256[] memory oracles) = oracleSequence.unpack();
        uint256 length = oracles.length;
        if (length == 0) revert RegistryErrors.Min1Oracle();
        ...
    }
```

## Tool used

Foundry

## Recommendation

Add a function like `setAssetInformation` to init the `assetInformation` directly in the contract `PrimaryAM`, or modify code so that `empty bytes` will not be used for `checkOracleSequence`.