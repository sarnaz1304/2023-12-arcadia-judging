Salty Tan Hamster

high

# AbstractStakingAM.sol#_getRewardBalances() - Incorrect logic inside reward calculation leads a revert, freezing user funds and miscalculating user rewards

## Summary
Incorrect logic inside reward calculation leads a revert, freezing user funds and miscalculating user rewards.

## Vulnerability Detail
`_getRewardBalances` is used to calculate the current global and position specific rewards for contracts that inherit from `AbstractStakingAM `

```solidity
function _getRewardBalances(AssetState memory assetState_, PositionState memory positionState_)
        internal
        view
        returns (AssetState memory, PositionState memory)
    {   
        if (assetState_.totalStaked > 0) {
            // Calculate the new assetState
            // Fetch the current reward balance from the staking contract.
            uint256 currentRewardGlobal = _getCurrentReward(positionState_.asset);
            // Calculate the increase in rewards since last Asset interaction.
            uint256 deltaReward = currentRewardGlobal - assetState_.lastRewardGlobal;
            uint256 deltaRewardPerToken = deltaReward.mulDivDown(1e18, assetState_.totalStaked);
            // Calculate and update the new RewardPerToken of the asset.
            // unchecked: RewardPerToken can overflow, what matters is the delta in RewardPerToken between two interactions.
            unchecked {
                assetState_.lastRewardPerTokenGlobal =
                    assetState_.lastRewardPerTokenGlobal + SafeCastLib.safeCastTo128(deltaRewardPerToken);
            }
            // Update the reward balance of the asset.
            assetState_.lastRewardGlobal = SafeCastLib.safeCastTo128(currentRewardGlobal);

            // Calculate the new positionState.
            // Calculate the difference in rewardPerToken since the last position interaction.
            // unchecked: RewardPerToken can underflow, what matters is the delta in RewardPerToken between two interactions.
            unchecked {
                deltaRewardPerToken = assetState_.lastRewardPerTokenGlobal - positionState_.lastRewardPerTokenPosition;
            }
            // Calculate the rewards earned by the position since its last interaction.
            // unchecked: deltaRewardPerToken and positionState_.amountStaked are smaller than type(uint128).max.
            unchecked {
                deltaReward = deltaRewardPerToken * positionState_.amountStaked / 1e18;
            }
            // Update the reward balance of the position.
            positionState_.lastRewardPosition =
                SafeCastLib.safeCastTo128(positionState_.lastRewardPosition + deltaReward);
        }
        // Update the RewardPerToken of the position.
        positionState_.lastRewardPerTokenPosition = assetState_.lastRewardPerTokenGlobal;

        return (assetState_, positionState_);
    }
```
In order to get the actual rewards, the function uses `_getCurrentReward` , which is a function that is supposed to be overridden by the contract inheriting `AbstractStakingAM`

One such class is `StakedStargateAM`  

```solidity
function _getCurrentReward(address asset) internal view override returns (uint256 currentReward) {
        currentReward = LP_STAKING_TIME.pendingEmissionToken(assetToPid[asset], address(this));
    }
```

The function makes an external call to Stargate’s `LPStakingTime.sol#pendingEmissionToken`

```solidity
function pendingEmissionToken(uint256 _pid, address _user) external view returns (uint256) {
        PoolInfo storage pool = poolInfo[_pid];
        UserInfo storage user = userInfo[_pid][_user];
        uint256 accEmissionPerShare = pool.accEmissionPerShare;
        uint256 lpSupply = pool.lpToken.balanceOf(address(this));
        if (block.timestamp > pool.lastRewardTime && lpSupply != 0 && totalAllocPoint > 0) {
            uint256 multiplier = getMultiplier(pool.lastRewardTime, block.timestamp);
            uint256 tokenReward = multiplier.mul(eTokenPerSecond).mul(pool.allocPoint).div(totalAllocPoint);
            accEmissionPerShare = accEmissionPerShare.add(tokenReward.mul(1e12).div(lpSupply));
        }
        return user.amount.mul(accEmissionPerShare).div(1e12).sub(user.rewardDebt);
    }
```

The protocol assumes that Stargate will always return a bigger value each time `pendingEmissionToken` is called, in a sort of linear manner, each time the function is called, the number will continue increasing.

Because of this assumption, the protocol manually calculates the increase in rewards.

```solidity
            // Calculate the increase in rewards since last Asset interaction.
            uint256 deltaReward = currentRewardGlobal - assetState_.lastRewardGlobal;
```

Taking another look at `pendingEmissionToken`

```solidity
function pendingEmissionToken(uint256 _pid, address _user) external view returns (uint256) {
        PoolInfo storage pool = poolInfo[_pid];
        UserInfo storage user = userInfo[_pid][_user];
        uint256 accEmissionPerShare = pool.accEmissionPerShare;
        uint256 lpSupply = pool.lpToken.balanceOf(address(this));
        if (block.timestamp > pool.lastRewardTime && lpSupply != 0 && totalAllocPoint > 0) {
            uint256 multiplier = getMultiplier(pool.lastRewardTime, block.timestamp);
            uint256 tokenReward = multiplier.mul(eTokenPerSecond).mul(pool.allocPoint).div(totalAllocPoint);
            accEmissionPerShare = accEmissionPerShare.add(tokenReward.mul(1e12).div(lpSupply));
        }
        return user.amount.mul(accEmissionPerShare).div(1e12).sub(user.rewardDebt);
    }
```

You’ll notice what the function returns:

`return user.amount.mul(accEmissionPerShare).div(1e12).sub(user.rewardDebt);`

It takes the `user.amount` which is the amount that `msg.sender` (`StakedStargateAM`) has deposited, then it multiplies it by the `accEmissionPerShare` which is calculated above, divides by `1e12` and then subtracts `user.rewardDebt`

`user.rewardDebt` acts as a checkpoint in a way to track rewards.

`user.rewardDebt` is set when `deposit` and `withdraw` are called inside `LPStakingTime`

We’ll take a look at only `deposit` as the logic is mirrored inside `withdraw`

```solidity
 function deposit(uint256 _pid, uint256 _amount) external {
        PoolInfo storage pool = poolInfo[_pid];
        UserInfo storage user = userInfo[_pid][msg.sender];
        updatePool(_pid);
        if (user.amount > 0) {
            uint256 pending = user.amount.mul(pool.accEmissionPerShare).div(1e12).sub(user.rewardDebt);
            safeTokenTransfer(msg.sender, pending);
        }
        pool.lpToken.safeTransferFrom(address(msg.sender), address(this), _amount);
        user.amount = user.amount.add(_amount);
        user.rewardDebt = user.amount.mul(pool.accEmissionPerShare).div(1e12);
        lpBalances[_pid] = lpBalances[_pid].add(_amount);
        emit Deposit(msg.sender, _pid, _amount);
    }
```

You’ll notice that `user.rewardDebt` is set to `user.amount.mul(pool.accEmissionPerShare).div(1e12)`

Going back to `pendingEmissionToken` you’ll see that the return basically just calculates the user’s new `rewardDebt` and then it subtracts his old `rewardDebt`

Stargate do this, because unlike most other staking contracts, Stargate transfers rewards on both a deposit and a withdraw, thus they calculate rewards like so.

This is the root of the problem, as it returns “current pending” rewards, not “total” rewards which the protocol assumes it will.

The example is very dumbed down, just to give an overview of the problem for simplicity.
Example:

1. Account1 stakes `1e18` tokens through `StakedStargateAM` 
2. `assetState.lastRewardGlobal` is `0` , as this is the first stake.
3. 10 days pass and Account2 stakes `1e18` 
4. Since `pendingEmissionToken` returns `100`
5. `deltaReward` will be `100`, since `100 - 0 = 100` 
6. 5 days pass and Account3 attempts to stake `1e18` tokens
7. `pendingEmissionToken` returns only `50`
8. The next line reverts, as we attempt `50 - 150` and since the value is `uint` the whole tx reverts.
9. The whole contract is now frozen until `pendingEmissionToken` returns a value `> 100` . All funds are stuck, no depositing/withdrawing or claiming of rewards can be done, since all three of these functions use `_getRewardBalances`

Because of the above, rewards will also be calculated incorrectly, a second PoC is attached demonstrating the issue.

## Impact
Affected functions are:
- `mint`
- `increaseLiquidity`
- `decreaseLiquidity`
- `claimReward`
- `rewardOf`

No depositing/withdrawing/claiming of rewards can be done. The bigger the difference between `lastRewardsGlobal` the bigger the DoS of the entire contract.

The second depositor can weaponize this and deposit after 30 days for example, to completely freeze the first depositor’s assets for 30 days. (Again this can be more/less than 30, it's an example)

But the issue will persist even after that and will continue growing, as each time `lastRewardGlobal` increases, the longer the DoS will be.

For example if `lastRewardGlobal = 1e18` , then only after `pendingEmissionToken` returns `> 1e18` will the function execute normally, which at that point `lastRewardGlobal` will be even bigger and so the next DoS will be even longer.

Rewards will also be incorrectly calculated, as showcased by the second PoC.

## Proof of Concept
To clearly see what is happening I recommend adding 2 console.logs after `_getCurrentReward` like so.

```solidity
function _getRewardBalances(AssetState memory assetState_, PositionState memory positionState_)
        internal
        view
        returns (AssetState memory, PositionState memory)
    {   
        if (assetState_.totalStaked > 0) {
            // Calculate the new assetState
            // Fetch the current reward balance from the staking contract.
            uint256 currentRewardGlobal = _getCurrentReward(positionState_.asset);
->          console.log("Current reward global: ", currentRewardGlobal);
->          console.log("Last reward global: ", assetState_.lastRewardGlobal);
            // Calculate the increase in rewards since last Asset interaction.
            uint256 deltaReward = currentRewardGlobal - assetState_.lastRewardGlobal;
            uint256 deltaRewardPerToken = deltaReward.mulDivDown(1e18, assetState_.totalStaked);
            // Calculate and update the new RewardPerToken of the asset.
            // unchecked: RewardPerToken can overflow, what matters is the delta in RewardPerToken between two interactions.
            unchecked {
                assetState_.lastRewardPerTokenGlobal =
                    assetState_.lastRewardPerTokenGlobal + SafeCastLib.safeCastTo128(deltaRewardPerToken);
            }
            // Update the reward balance of the asset.
            assetState_.lastRewardGlobal = SafeCastLib.safeCastTo128(currentRewardGlobal);

            // Calculate the new positionState.
            // Calculate the difference in rewardPerToken since the last position interaction.
            // unchecked: RewardPerToken can underflow, what matters is the delta in RewardPerToken between two interactions.
            unchecked {
                deltaRewardPerToken = assetState_.lastRewardPerTokenGlobal - positionState_.lastRewardPerTokenPosition;
            }
            // Calculate the rewards earned by the position since its last interaction.
            // unchecked: deltaRewardPerToken and positionState_.amountStaked are smaller than type(uint128).max.
            unchecked {
                deltaReward = deltaRewardPerToken * positionState_.amountStaked / 1e18;
            }
            // Update the reward balance of the position.
            positionState_.lastRewardPosition =
                SafeCastLib.safeCastTo128(positionState_.lastRewardPosition + deltaReward);
        }
        // Update the RewardPerToken of the position.
        positionState_.lastRewardPerTokenPosition = assetState_.lastRewardPerTokenGlobal;

        return (assetState_, positionState_);
    }
```

Paste the following inside `USDbCPool.fork.t.sol` and run `forge test --mt test_getRewardBalancesBreaksForStargate -vvvv`

```solidity
function test_getRewardBalancesBreaksForStargate() public {
        // Amount of underlying assets deposited in Stargate pool.
        uint256 amount1 = 1_000_000 * 10 ** USDbC.decimals();
        uint256 amount2 = 123_456 * 10 ** USDbC.decimals();

        // 2 users deploy a new Arcadia Account.
        address payable user1 = createUser("user1");
        address payable user2 = createUser("user2");

        vm.prank(user1);
        address arcadiaAccount1 = factory.createAccount(100, 0, address(0));

        vm.prank(user2);
        address arcadiaAccount2 = factory.createAccount(101, 0, address(0));

        // Stake Stargate Pool LP tokens in the Asset Modules and deposit minted ERC721 in Accounts.
        uint256 lpBalance1 = stakeInAssetModuleAndDepositInAccount(user1, arcadiaAccount1, USDbC, amount1, pid, pool);
        // 30 days pass and arcadiaAccount2 stakes as well
        vm.warp(block.timestamp + 30 days);
        uint256 lpBalance2 = stakeInAssetModuleAndDepositInAccount(user2, arcadiaAccount2, USDbC, amount2, pid, pool);

        // 20 days pass and account1 wants to decrease his liquidity
        vm.warp(block.timestamp + 20 days);

        vm.startPrank(arcadiaAccount1);
        // The tx reverts with an underflow
        vm.expectRevert();
        // The amount is irrelevant as we don't even reach that part of the code
        stakedStargateAM.decreaseLiquidity(1, 100);

        // The type of function doesn't matter as they all call _getRewardBalances
        vm.expectRevert();
        stakedStargateAM.burn(1);
        vm.stopPrank();
     }
```

PoC showcasing incorrect reward distribution.
```jsx
function test_IncorrectHandlingOfRewards() public {
        uint256 initBalance = 1000 * 10 ** USDbC.decimals();
        assert(ERC20(address(pool)).balanceOf(users.accountOwner) == 0);

        // Given : A user deposits in the Stargate USDbC pool, in exchange of an LP token.
        vm.startPrank(users.accountOwner);
        deal(address(USDbC), users.accountOwner, initBalance);

        USDbC.approve(address(router), initBalance);
        router.addLiquidity(poolId, initBalance, users.accountOwner);

        // And : The user stakes the LP token via the StargateAssetModule
        uint256 stakedAmountFirst = ERC20(address(pool)).balanceOf(users.accountOwner);
        console.log(stakedAmountFirst);
        ERC20(address(pool)).approve(address(stakedStargateAM), stakedAmountFirst);
        uint256 tokenId = stakedStargateAM.mint(address(pool), uint128(stakedAmountFirst));

        vm.warp(block.timestamp + 50 days);

        deal(address(USDbC), users.accountOwner, initBalance * 2);
        USDbC.approve(address(router), initBalance * 2);
        router.addLiquidity(poolId, initBalance * 2, users.accountOwner);

        uint256 stakedAmountSecond = ERC20(address(pool)).balanceOf(users.accountOwner) - stakedAmountFirst;
        console.log( ERC20(address(pool)).balanceOf(users.accountOwner));
        ERC20(address(pool)).approve(address(stakedStargateAM), stakedAmountSecond);
        stakedStargateAM.increaseLiquidity(tokenId, uint128(stakedAmountSecond));


        vm.warp(block.timestamp + 100 days);
        stakedStargateAM.claimReward(tokenId);

        ERC20 rewardToken = stakedStargateAM.REWARD_TOKEN();
        assertEq(rewardToken.balanceOf(address(stakedStargateAM)), 0);

        vm.stopPrank();
    }
```

## Code Snippet
https://github.com/arcadia-finance/accounts-v2/blob/9b24083cb832a41fce609a94c9146e03a77330b4/src/asset-modules/abstracts/AbstractStakingAM.sol#L539

## Tool used
Manual Review
Foundry

## Recommendation
Since `AbstractStakingAM` is used as a base class, I recommend making `_getRewardBalances` virtual so that each implementation can override it.