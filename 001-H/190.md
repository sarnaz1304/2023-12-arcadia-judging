Expert Daisy Troll

high

# Incorrect accounting for rewards in mint and increaseLiquidity

## Summary

The current implementation of the `mint` and `increaseLiquidity` functions in the `StakedStargateAM` contract fails to account for rewards received during these operations. This issue can lead to a denial of service (DoS) condition for the `StakedStargateAM` contract and inaccuracies in reward calculations.

## Vulnerability Detail

When `mint` and `increaseLiquidity`, the staking asset module `StakedStargateAM` call the internal `_stake` function.

        function mint(address asset, uint128 amount) external virtual nonReentrant returns (uint256 positionId) {
            ...

            // Stake Asset in external staking contract.
            _stake(asset, amount);

            // Mint the new position.
            _safeMint(msg.sender, positionId);

            emit LiquidityIncreased(positionId, asset, amount);
        }

https://github.com/arcadia-finance/accounts-v2/blob/83eef2ef44a46a19e46b3d007929b5ea64db4789/src/asset-modules/abstracts/AbstractStakingAM.sol#L285-L320

        function increaseLiquidity(uint256 positionId, uint128 amount) external virtual nonReentrant {
            ...

            // Stake Asset in external staking contract.
            _stake(asset, amount);

            emit LiquidityIncreased(positionId, asset, amount);
        }

https://github.com/arcadia-finance/accounts-v2/blob/83eef2ef44a46a19e46b3d007929b5ea64db4789/src/asset-modules/abstracts/AbstractStakingAM.sol#L327-L354

The `_stake` function deposits assets to the StarGate LPStakingTime contract.

        function _stake(address asset, uint256 amount) internal override {
            ERC20(asset).approve(address(LP_STAKING_TIME), amount);

            // Stake asset
            LP_STAKING_TIME.deposit(assetToPid[asset], amount); // when call deposit and withdraw the depositor receive reward token
        }

Reviewing the LPStakingTime.deposit, we can see that each time the `deposit` is called, the pending reward token amounts are transferred to the depositor.

        function deposit(uint256 _pid, uint256 _amount) external {
            PoolInfo storage pool = poolInfo[_pid];
            UserInfo storage user = userInfo[_pid][msg.sender];
            updatePool(_pid);
            if (user.amount > 0) {
                uint256 pending = user.amount.mul(pool.accEmissionPerShare).div(1e12).sub(user.rewardDebt);
                safeTokenTransfer(msg.sender, pending);
            }
            pool.lpToken.safeTransferFrom(address(msg.sender), address(this), _amount);
            user.amount = user.amount.add(_amount);
            user.rewardDebt = user.amount.mul(pool.accEmissionPerShare).div(1e12);
            lpBalances[_pid] = lpBalances[_pid].add(_amount);
            emit Deposit(msg.sender, _pid, _amount);
        }

https://basescan.org/address/0x06eb48763f117c7be887296cdcdfad2e4092739c#code

During the `deposit` operation in `LPStakingTime`, rewards are sent to the depositor, but these rewards are not accounted for in the `mint` and `increaseLiquidity` functions. It leads to wrong value of `assetState_.lastRewardGlobal` which should be reset to 0 after each reward received.

It leads to a problem when calculating the reward balance inside the StakedStargateAM. When calculating `deltaReward = currentRewardGlobal - assetState_.lastRewardGlobal`:
- the `currentRewardGlobal` is the pending reward received amount, it resets to 0 each time reward is sending.
- `assetState_.lastRewardGlobal` equals to `currentRewardGlobal` (last pending reward amount).

So the `currentRewardGlobal` can be less than `assetState_.lastRewardGlobal` which results in the `_getRewardBalances` function reverting transactions and under-calculating rewards for stakers.

        function _getRewardBalances(AssetState memory assetState_, PositionState memory positionState_)
            internal
            view
            returns (AssetState memory, PositionState memory)
        {
            if (assetState_.totalStaked > 0) {
                // Calculate the new assetState
                // Fetch the current reward balance from the staking contract.
                uint256 currentRewardGlobal = _getCurrentReward(positionState_.asset);
                // Calculate the increase in rewards since last Asset interaction.
                uint256 deltaReward = currentRewardGlobal - assetState_.lastRewardGlobal;
                ...
                // Update the reward balance of the asset.
                assetState_.lastRewardGlobal = SafeCastLib.safeCastTo128(currentRewardGlobal);
                ...

            return (assetState_, positionState_);
        }

https://github.com/arcadia-finance/accounts-v2/blob/83eef2ef44a46a19e46b3d007929b5ea64db4789/src/asset-modules/abstracts/AbstractStakingAM.sol#L529-L569


This issue can render the `StakedStargateAM` contract non-operational (DoS), preventing successful execution of `mint`, `increaseLiquidity`, `decreaseLiquidity`, and `claimReward` functions since those functions call `_getRewardBalances`.

## Impact

The StakedStargateAM is DOS and `mint`, `increaseLiquidity`, `decreaseLiquidity`, `claimReward` will always revert. Rewards are less calculated for stakers.

## Code Snippet

https://github.com/arcadia-finance/accounts-v2/blob/83eef2ef44a46a19e46b3d007929b5ea64db4789/src/asset-modules/abstracts/AbstractStakingAM.sol#L285-L320
https://github.com/arcadia-finance/accounts-v2/blob/83eef2ef44a46a19e46b3d007929b5ea64db4789/src/asset-modules/abstracts/AbstractStakingAM.sol#L327-L354
https://github.com/arcadia-finance/accounts-v2/blob/83eef2ef44a46a19e46b3d007929b5ea64db4789/src/asset-modules/abstracts/AbstractStakingAM.sol#L529-L569

## Tool used

Manual Review

## Recommendation

Update the `assetState_.lastRewardGlobal` each time `_stake` like in `decreaseLiquidity` function to reflect the reward received amount.