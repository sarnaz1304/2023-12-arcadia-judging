Salty Tan Hamster

medium

# AccountV1.sol#upgradeAccount() - When upgrading an account to a new version the numeraire, minimumMargin and liquidator are not checked if they have been changed

## Summary
When upgrading an account to a new version the numeraire, minimumMargin and liquidator are not checked if they have been changed

## Vulnerability Detail
When an owner creates an Arcadia Account he can choose a Creditor.

```solidity
function initialize(address owner_, address registry_, address creditor_) external {
        if (registry != address(0)) revert AccountErrors.AlreadyInitialized();
        if (registry_ == address(0)) revert AccountErrors.InvalidRegistry();
        owner = owner_;
        registry = registry_;

        if (creditor_ != address(0)) _openMarginAccount(creditor_);

        locked = 1;
    }
```

Inside `initialize` we call `_openMarginAccount` . The function is used to set 3 very important variables for an Account. The `numeraire, minimumMargin and liquidator`  which all come from the Creditor.

```solidity
function _openMarginAccount(address creditor_) internal {
        (bool success, address numeraire_, address liquidator_, uint256 minimumMargin_) =
            ICreditor(creditor_).openMarginAccount(ACCOUNT_VERSION);
        if (!success) revert AccountErrors.InvalidAccountVersion();

        minimumMargin = uint96(minimumMargin_);
        if (numeraire != numeraire_) _setNumeraire(numeraire_);

        emit MarginAccountChanged(creditor = creditor_, liquidator = liquidator_);
    }
```

The function sets all 3 variables that were retrieved from the Creditor. We’ll use the `LendingPool` as an example, as currently that’s the only contract that implements the Creditor.

```solidity
function openMarginAccount(uint256 accountVersion)
        external
        view
        override
        returns (bool success, address numeraire, address liquidator_, uint256 minimumMargin_)
    {
        if (isValidVersion[accountVersion]) {
            success = true;
            numeraire = address(asset);
            liquidator_ = LIQUIDATOR;
            minimumMargin_ = minimumMargin;
        }
    }
```

When an owner of an Account wants to upgrade to a new version, they call `upgradeAccountVersion` , which then forwards the call to `upgradeAccount`  inside the actual Arcadia Account.

```solidity
function upgradeAccount(address newImplementation, address newRegistry, uint256 newVersion, bytes calldata data)
        external
        onlyFactory
        nonReentrant
        notDuringAuction
        updateActionTimestamp
    {
        // Cache old parameters.
        address oldImplementation = _getAddressSlot(IMPLEMENTATION_SLOT).value;
        address oldRegistry = registry;
        uint256 oldVersion = ACCOUNT_VERSION;

        // Store new parameters.
        _getAddressSlot(IMPLEMENTATION_SLOT).value = newImplementation;
        registry = newRegistry;

        // Prevent that Account is upgraded to a new version where the Numeraire can't be priced.
        if (newRegistry != oldRegistry && !IRegistry(newRegistry).inRegistry(numeraire)) {
            revert AccountErrors.InvalidRegistry();
        }

        // If a Creditor is set, new version should be compatible.
        if (creditor != address(0)) {
            (bool success,,,) = ICreditor(creditor).openMarginAccount(newVersion);
            if (!success) revert AccountErrors.InvalidAccountVersion();
        }

        // Hook on the new logic to finalize upgrade.
        // Used to eg. Remove exposure from old Registry and add exposure to the new Registry.
        // Extra data can be added by the Factory for complex instructions.
        this.upgradeHook(oldImplementation, oldRegistry, oldVersion, data);

        // Event emitted by Factory.
    }
```

You’ll notice the if statement, `creditor != address(0)` and that it calls `openMarginAccount`  on the current creditor’s address.

```solidity
if (creditor != address(0)) {
            (bool success,,,) = ICreditor(creditor).openMarginAccount(newVersion);
            if (!success) revert AccountErrors.InvalidAccountVersion();
        }
```

The protocol team has stated, that this function is used to “ask” the Creditor if they support the `newVersion`  and if they do, they will return `success = true` , and that currently the `LendingPool` doesn’t change it’s `numeraire, liquidator or minimumMargin`  based on the `accountVersion` that is passed to the function.

This is incorrect, as `minimumMargin`  can change, through `setMinimumMargin` 

```solidity
function setMinimumMargin(uint96 minimumMargin_) external onlyOwner {
        minimumMargin = minimumMargin_;
    }
```

Knowing that `minimumMargin`  can change, we see the real problem. When an account owner wants to upgrade to a new version, the `minimumMargin` that the Creditor is currently using isn’t set as the `minimumMargin`  inside the actual Account, and the two contracts fall out of sync.
## Impact
Affected functions are: 
- `isAccountUnhealthy`
- `getUsedMargin`
- `increaseOpenPosition`
- `startLiquidation`
- `isAccountLiquidatable`

## Code Snippet
https://github.com/sherlock-audit/2023-12-arcadia/blob/de7289bebb3729505a2462aa044b3960d8926d78/accounts-v2/src/accounts/AccountV1.sol#L214-L217

## Tool used
Manual Review

## Recommendation
Check if the 3 variables have been changed and set them, when upgrading an Account to a new verison.