Powerful Myrtle Hawk

medium

# User could create non-sense `proxy` before `versionInformation` is ever set

## Summary
The [ check in Factory::createAccount](https://github.com/sherlock-audit/2023-12-arcadia/blob/main/accounts-v2/src/Factory.sol#L89-L92) will use `the newest version` when `accountVersion == 0`. However, there is a lack of check that `latestAccountVersion` should not be `0` as no `versionInformation` is ever set at this point. Because of this, before the function `Factory::setNewAccountInfo` is ever called, a user could still call `Factory::createAccount` without reverting. The returned `proxy` will always delegate to `address(0)` and is nonsense. However, the NFT will still be minted and still be counted as valid accounts.

## Vulnerability Detail
For [Factory::createAccount](https://github.com/sherlock-audit/2023-12-arcadia/blob/main/accounts-v2/src/Factory.sol#L89-L92) , user can chose to set `accountVersion` as `0` to switch to `latestAccountVersion`.
```solidity
        accountVersion = accountVersion == 0 ? latestAccountVersion : accountVersion;

        if (accountVersion > latestAccountVersion) revert FactoryErrors.InvalidAccountVersion();
        if (accountVersionBlocked[accountVersion]) revert FactoryErrors.AccountVersionBlocked();
```
The `accountVersion` should not exceed `latestAccountVersion` and should not be blocked.

However, when the function [Factory::setNewAccountInfo](https://github.com/sherlock-audit/2023-12-arcadia/blob/main/accounts-v2/src/Factory.sol#L263C14-L263C31) is not called, `latestAccountVersion` would remain to be `0`. If `accountVersion == 0`,  since `accountVersion == latestAccountVersion` and `accountVersionBlocked[0] == false`, the requirement can be bypassed.

Since `versionInformation[accountVersion].implementation` will be `address(0)` if no value is set before, the [Proxy](https://github.com/sherlock-audit/2023-12-arcadia/blob/main/accounts-v2/src/Proxy.sol#L41C1-L44C6) created will take `address(0)`  as the address in `IMPLEMENTATION_SLOT` to delegate to. So this proxy is of non-sense and can't be used.

```solidity
    constructor(address implementation) payable {
        _getAddressSlot(IMPLEMENTATION_SLOT).value = implementation;
        emit Upgraded(implementation);
    }
```
Since `AccountV1::initialize` has no return value, the code will not revert. `NFT` will be minted and `accounts` will be recognized as `valid`.

```solidity
        allAccounts.push(account);
        accountIndex[account] = allAccounts.length; // What about accountIndex = 0?

        _mint(msg.sender, allAccounts.length);

        IAccount(account).initialize(msg.sender, versionInformation[accountVersion].registry, creditor);
```

We have a PoC here:

comment `factory.setNewAccountInfo(address(registryExtension), address(accountV1Logic), Constants.upgradeProof1To2, "");` in the `setUp()` function.

```solidity
    function test_createAccountWithoutAccount() public {
        console2.log("current latestAccountVersion", factory.latestAccountVersion());
        assertEq(factory.latestAccountVersion(),0);
        address proxyAddress = factory.createAccount(0, 0, address(0));
        console2.log("proxyAddress ", proxyAddress);
        AccountV1 proxyAccount = AccountV1(proxyAddress);
        assertEq(factory.allAccountsLength(),1);  // nft will still be minted
        proxyAccount.setNumeraire(address(this)); // Delegate Call on EOA won't revert
        factory.createAccount(1, 0, address(0));
        assertEq(factory.allAccountsLength(),2);
    }
```
The result is shown below:
![image](https://github.com/sherlock-audit/2023-12-arcadia-jes16jupyter/assets/153517808/681e1817-f132-4788-9757-896421505116)

## Impact
The current check is flawed and could be bypassed when no `versionInformation` has been set.  Thus `Proxy` delegating to `address(0)` will be created. Even if it is nonsense and should not be valid, the NFT will still be minted and still be counted as a valid account.

## Code Snippet
[Factory::createAccount](https://github.com/sherlock-audit/2023-12-arcadia/blob/main/accounts-v2/src/Factory.sol#L84-L111)
```solidity
    function createAccount(uint256 salt, uint256 accountVersion, address creditor)
        external
        whenCreateNotPaused
        returns (address account)
    {
        accountVersion = accountVersion == 0 ? latestAccountVersion : accountVersion;

        if (accountVersion > latestAccountVersion) revert FactoryErrors.InvalidAccountVersion();
        if (accountVersionBlocked[accountVersion]) revert FactoryErrors.AccountVersionBlocked();

        // Hash tx.origin with the user-provided salt to avoid front-running Account deployment with an identical salt.
        // We use tx.origin instead of msg.sender so that deployments through a third party contract are not vulnerable to front-running.
        account = address(
            new Proxy{ salt: keccak256(abi.encodePacked(salt, tx.origin)) }(
                versionInformation[accountVersion].implementation
            )
        );

        allAccounts.push(account);
        accountIndex[account] = allAccounts.length;

        _mint(msg.sender, allAccounts.length);

        IAccount(account).initialize(msg.sender, versionInformation[accountVersion].registry, creditor);

        // unsafe cast: accountVersion <= latestAccountVersion, which is a uint88.
        emit AccountUpgraded(account, uint88(accountVersion));
    }
```
[Proxy::constructor](https://github.com/sherlock-audit/2023-12-arcadia/blob/main/accounts-v2/src/Proxy.sol#L41-L44)
```solidity
    constructor(address implementation) payable {
        _getAddressSlot(IMPLEMENTATION_SLOT).value = implementation;
        emit Upgraded(implementation);
    }
```

## Tool used
Foundry

Manual Review

## Recommendation
A few ways to mitigate the issue.
1. Add a check to ensure that `latestAccountVersion` is not `0`.
2. Block version `0` by default.