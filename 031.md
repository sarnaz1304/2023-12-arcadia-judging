Powerful Myrtle Hawk

high

# The owner can bypass the check of `notDuringAuction` and `isAccountUnhealthy` and drain some colleteral tokens using `skim`

## Summary
The function [AccountV1::skim](https://github.com/sherlock-audit/2023-12-arcadia/blob/main/accounts-v2/src/accounts/AccountV1.sol#L1183-L1188) is performed by the `owner` to **skims non-deposited assets from the Account**. For this operation, there is no check of `notDuringAuction` nor `isAccountUnhealthy` since the tokens have the extra amount and are not counted as collateral. However, if a collateral token has multiple addresses/entrances (like TrueUSD,...), the owner could use the other address as `token` and drain the corresponding collateral token amount, bypassing the check of `notDuringAuction` and `isAccountUnhealthy`.

## Vulnerability Detail
In the function [AccountV1::skim](https://github.com/sherlock-audit/2023-12-arcadia/blob/main/accounts-v2/src/accounts/AccountV1.sol#L1183-L1188), the 
non-deposited assets will be returned to the owner. 
```solidity
        if (type_ == 0) {
            uint256 balance = ERC20(token).balanceOf(address(this));
            uint256 balanceStored = erc20Balances[token];
            if (balance > balanceStored) {
                ERC20(token).safeTransfer(msg.sender, balance - balanceStored);
            }
        }
```
Since the assets are not counted as collateral, so the function has no check of `notDuringAuction` nor `isAccountUnhealthy`.
```solidity
    function skim(address token, uint256 id, uint256 type_) public onlyOwner nonReentrant updateActionTimestamp {
    ...
    }
```

However,  **some ERC20 tokens (like TrueUSD) have multiple addresses/entrances, all pointing to the same underlying address.** If a token of this is used as the collateral, calling `skim` with another entrance will definitely have different `ERC20(token).balanceOf(address(this))` and `erc20Balances[token]`. Thus, the original collateral token has been drained by the owner. 

This could happen when the `account` is being liquidated and no `withdraw` could be done by the `owner` due to `notDuringAuction` check. Also, if the owner gets tricked into doing this, his account may get into `unhealthy` state without `isAccountUnhealthy` check and could be liquidated.

## Impact
If a token with multiple addresses/entrances is used as the collateral, calling `skim` with another entrance will have different `ERC20(token).balanceOf(address(this))` and `erc20Balances[token]`. Thus, the original collateral token has been drained by the owner. 

If the `account` is being liquidated and no `withdraw` could be done by the `owner` due to `notDuringAuction` check, the owner could use this to drain some tokens from the account.

Also, If the owner is tricked into doing this, his account may get into `unhealthy` state without `isAccountUnhealthy` check and could be liquidated.

## Code Snippet

[AccountV1::skim](https://github.com/sherlock-audit/2023-12-arcadia/blob/main/accounts-v2/src/accounts/AccountV1.sol#L1176C1-L1210C6)

```solidity
    function skim(address token, uint256 id, uint256 type_) public onlyOwner nonReentrant updateActionTimestamp {
        if (token == address(0)) {
            (bool success, bytes memory result) = payable(msg.sender).call{ value: address(this).balance }("");
            require(success, string(result));
            return;
        }

        if (type_ == 0) {
            uint256 balance = ERC20(token).balanceOf(address(this));
            uint256 balanceStored = erc20Balances[token];
            if (balance > balanceStored) {
                ERC20(token).safeTransfer(msg.sender, balance - balanceStored);
            }
        } else if (type_ == 1) {
            bool isStored;
            uint256 erc721StoredLength = erc721Stored.length;
            for (uint256 i; i < erc721StoredLength; ++i) {
                if (erc721Stored[i] == token && erc721TokenIds[i] == id) {
                    isStored = true;
                    break;
                }
            }

            if (!isStored) {
                IERC721(token).safeTransferFrom(address(this), msg.sender, id);
            }
        } else if (type_ == 2) {
            uint256 balance = IERC1155(token).balanceOf(address(this), id);
            uint256 balanceStored = erc1155Balances[token][id];

            if (balance > balanceStored) {
                IERC1155(token).safeTransferFrom(address(this), msg.sender, id, balance - balanceStored, "");
            }
        }
    }
```

## Tool used

Manual Review, VSCode

## Recommendation
Add a check to ensure that `getCollateralValue()` before and after the `skim` is not changed.