Quiet Khaki Beetle

high

# Rewards for StakedStargateAM is calculated incorrectly

## Summary

In the `StakedStargateAM` contract, `deltaReward` is supposed to reflect the increase in rewards after each interaction with the external staking contract. However, it's miscalculated, leading to entirely incorrect reward calculations and possibly causing standard staking operations (like minting) in `StakedStargateAM` to revert.

## Vulnerability Detail

The following code snippets illustrate that the `pendingEmissionToken` function for Stargate returns the pending reward tokens, rather than the total reward tokens. This discrepancy leads to an inaccurate calculation of `deltaReward` within `AbstractStakingAM.sol`, and even may result to integer underflow. To fix this, the correct implementation should either:

1. Adjust the `_getCurrentReward` function to return the correct total amount of reward tokens.
2. Assume `_getCurrentReward` to return the pending tokens and then simply set `deltaReward = _getCurrentReward(positionState_.asset)`.

[AbstractStakingAM.sol](https://github.com/sherlock-audit/2023-12-arcadia/blob/main/accounts-v2/src/asset-modules/abstracts/AbstractStakingAM.sol#L537-L539)
```solidity
            uint256 currentRewardGlobal = _getCurrentReward(positionState_.asset);
            // Calculate the increase in rewards since last Asset interaction.
>           uint256 deltaReward = currentRewardGlobal - assetState_.lastRewardGlobal;
            ...
            assetState_.lastRewardGlobal = SafeCastLib.safeCastTo128(currentRewardGlobal);
```

[StakedStargateAM.sol](https://github.com/sherlock-audit/2023-12-arcadia/blob/main/accounts-v2/src/asset-modules/Stargate-Finance/StakedStargateAM.sol#L113-L115)
```solidity
    function _getCurrentReward(address asset) internal view override returns (uint256 currentReward) {
        currentReward = LP_STAKING_TIME.pendingEmissionToken(assetToPid[asset], address(this));
    }
```

[Stargate LPStakingTime.sol](https://etherscan.io/address/0x1c3000b8f475a958b87c73a5cc5780ab763122fc#code)
```solidity
    function pendingEmissionToken(uint256 _pid, address _user) external view returns (uint256) {
        PoolInfo storage pool = poolInfo[_pid];
        UserInfo storage user = userInfo[_pid][_user];
        uint256 accEmissionPerShare = pool.accEmissionPerShare;
        uint256 lpSupply = pool.lpToken.balanceOf(address(this));
        if (block.timestamp > pool.lastRewardTime && lpSupply != 0 && totalAllocPoint > 0) {
            uint256 multiplier = getMultiplier(pool.lastRewardTime, block.timestamp);
            uint256 tokenReward = multiplier.mul(eTokenPerSecond).mul(pool.allocPoint).div(totalAllocPoint);
            accEmissionPerShare = accEmissionPerShare.add(tokenReward.mul(1e12).div(lpSupply));
        }
        return user.amount.mul(accEmissionPerShare).div(1e12).sub(user.rewardDebt);
    }
```

## Impact


The direct impact is that the calculation of Stargate reward tokens is entirely incorrect. Furthermore, this could result in an integer underflow at the line `uint256 deltaReward = currentRewardGlobal - assetState_.lastRewardGlobal;`, potentially disrupting standard staking actions, such as minting.

This issue is demonstrated by the following test code, which can be incorporated into the `USDbCPool.fork.t.sol` file for execution and validation that the last mint action indeed leads to an integer underflow.

```solidity
    function testFork_Mint_Fail() public {
        // 1. Create 3 users and their Arcadia accounts respectively.
        uint256 amount1 = 1_000_000 * 10 ** USDbC.decimals();
        uint256 amount2 = 1_000_00 * 10 ** USDbC.decimals();
        uint256 amount3 = 1_000 * 10 ** USDbC.decimals();

        address payable user1 = createUser("user1");
        address payable user2 = createUser("user2");
        address payable user3 = createUser("user2");

        vm.prank(user1);
        address arcadiaAccount1 = factory.createAccount(100, 0, address(0));
        vm.prank(user2);
        address arcadiaAccount2 = factory.createAccount(101, 0, address(0));
        vm.prank(user3);
        address arcadiaAccount3 = factory.createAccount(102, 0, address(0));

        // 2. The 3 users mint in StakedStargateAM, but the last mint fails due to integer underflow.
        uint256 lpBalance1 = stakeInAssetModuleAndDepositInAccount(user1, arcadiaAccount1, USDbC, amount1, pid, pool);
        vm.warp(block.timestamp + 30 days);
        uint256 lpBalance2 = stakeInAssetModuleAndDepositInAccount(user2, arcadiaAccount2, USDbC, amount2, pid, pool);
        vm.warp(block.timestamp + 1 days);
        uint256 lpBalance3 = stakeInAssetModuleAndDepositInAccount(user3, arcadiaAccount3, USDbC, amount3, pid, pool);
    }
```

## Code Snippet

- https://github.com/sherlock-audit/2023-12-arcadia/blob/main/accounts-v2/src/asset-modules/abstracts/AbstractStakingAM.sol#L537-L539
- https://github.com/sherlock-audit/2023-12-arcadia/blob/main/accounts-v2/src/asset-modules/Stargate-Finance/StakedStargateAM.sol#L113-L115
- https://etherscan.io/address/0x1c3000b8f475a958b87c73a5cc5780ab763122fc#code

## Tool used

VSCode, Foundry

## Recommendation

There are two ways for mitigation:

1. Consider having `_getCurrentReward` return the pending reward tokens and simply set `deltaReward = _getCurrentReward(positionState_.asset)`:

```diff
         if (assetState_.totalStaked > 0) {
-            // Calculate the new assetState
-            // Fetch the current reward balance from the staking contract.
-            uint256 currentRewardGlobal = _getCurrentReward(positionState_.asset);
             // Calculate the increase in rewards since last Asset interaction.
-            uint256 deltaReward = currentRewardGlobal - assetState_.lastRewardGlobal;
+            uint256 deltaReward = _getCurrentReward(positionState_.asset);
             uint256 deltaRewardPerToken = deltaReward.mulDivDown(1e18, assetState_.totalStaked);
             // Calculate and update the new RewardPerToken of the asset.
```

2. Continue to have the `_getCurrentReward` function return the total amount of reward tokens, which would require tracking the count within `StakedStargateAM`, given that Stargate lacks a function for this purpose.