Docile Mahogany Spider

high

# The StakedStargateAM is unable to distribute user rewards correctly.

## Summary
Users can `stake` their `assets` to the `Stargate staking pool` to earn `rewards` through `StakedStargateAM`. 
Over time, the value of this `asset` increases as `rewards` accumulate. 
However, there is an error in the `staking logic`.
## Vulnerability Detail
Whenever there is a change in the user's `balance` through adding or removing `liquidity`, we should update the current available `rewards` from the `Stargate staking pool` and then proceed with the `liquidity` adjustment. 
This process is handled in the following function.
```solidity
function _getRewardBalances(AssetState memory assetState_, PositionState memory positionState_) {
    uint256 currentRewardGlobal = _getCurrentReward(positionState_.asset);
    uint256 deltaReward = currentRewardGlobal - assetState_.lastRewardGlobal;
    uint256 deltaRewardPerToken = deltaReward.mulDivDown(1e18, assetState_.totalStaked);

    assetState_.lastRewardGlobal = SafeCastLib.safeCastTo128(currentRewardGlobal);
}
```
We fetch the available `reward` amount from the `Stargate pool` after the last update.
```solidity
function _getCurrentReward(address asset) internal view override returns (uint256 currentReward) {
    currentReward = LP_STAKING_TIME.pendingEmissionToken(assetToPid[asset], address(this));
}
```
Then, we calculate the `global reward` and `rewards` for individual users. 
This is a common pattern in `staking` contracts.

However, there is an issue when adding `liquidity`. 

When we withdraw `liquidity`, the `Stargate staking pool` updates the `pool` first and then sends the available `rewards` to the user.(In this case, user is our StakedStargateAM module.)
```solidity
function withdraw(uint256 _pid, uint256 _amount) public {
    updatePool(_pid);
    uint256 pending = user.amount.mul(pool.accStargatePerShare).div(1e12).sub(user.rewardDebt);
    safeStargateTransfer(msg.sender, pending);
}
```
So, the pending `reward` from `Stargate staking pool` becomes `0`. 
In our `StakedStargateAM`, we properly update `lastRewardGlobal` as `0` in the `decreaseLiquidity` function, ensuring that everything works correctly.
```solidity
function decreaseLiquidity(uint256 positionId, uint128 amount) {
    (assetState_, positionState_) = _getRewardBalances(assetState_, positionState_);
    rewards = positionState_.lastRewardPosition;
    positionState_.lastRewardPosition = 0;
    assetState_.lastRewardGlobal = 0;   // @audit, here

    _withdraw(asset, amount);
    _claimReward(asset);
}
```

Also the `Stargate staking pool` sends the `pending rewards` to the user when the user deposits `liquidity`.
```solidity
function deposit(uint256 _pid, uint256 _amount) public {
    updatePool(_pid);
    if (user.amount > 0) {
        uint256 pending = user.amount.mul(pool.accStargatePerShare).div(1e12).sub(user.rewardDebt);
        safeStargateTransfer(msg.sender, pending);
    }
}
```
So, the pending `reward` from `Stargate staking pool` becomes `0`. 
However in our `StakedStargateAM`, we don't set `lastRewardGlobal` to `0` when increasing `liquidity`.
So, if `lastRewardGlobal` becomes large during some period and any user tries to add `liquidity`, the `pending rewards` from the `Stargate staking pool` become `0`. 
Therefore, all actions will be reverted until the `pending rewards` become larger than `lastRewardGlobal` because any action will invoke the `_getRewardBalances` function.
```solidity
function _getRewardBalances(AssetState memory assetState_, PositionState memory positionState_) {
    uint256 currentRewardGlobal = _getCurrentReward(positionState_.asset);
    // Calculate the increase in rewards since last Asset interaction.
    uint256 deltaReward = currentRewardGlobal - assetState_.lastRewardGlobal;  // @audit, revert here
}
```
The serious problem is that calculating the price of this `asset` also calls the `_getRewardBalances` function, causing the `account` to be unusable for some period.

And until the `pending rewards` from the `Stargate staking pool` become larger than `lastRewardGlobal`, users cannot receive any `rewards`. 
In other words, the `rewards` equal to `lastRewardGlobal` are not distributed to the users.
## Impact

## Code Snippet
https://github.com/sherlock-audit/2023-12-arcadia/blob/main/accounts-v2/src/asset-modules/abstracts/AbstractStakingAM.sol#L537-L540
https://github.com/sherlock-audit/2023-12-arcadia/blob/main/accounts-v2/src/asset-modules/Stargate-Finance/StakedStargateAM.sol#L113-L115
https://github.com/stargate-protocol/stargate/blob/c647a3a647fc693c38b16ef023c54e518b46e206/contracts/LPStaking.sol#L172-L174
https://github.com/sherlock-audit/2023-12-arcadia/blob/main/accounts-v2/src/asset-modules/abstracts/AbstractStakingAM.sol#L394-L396
https://github.com/stargate-protocol/stargate/blob/c647a3a647fc693c38b16ef023c54e518b46e206/contracts/LPStaking.sol#L156-L160
https://github.com/sherlock-audit/2023-12-arcadia/blob/main/accounts-v2/src/asset-modules/abstracts/AbstractStakingAM.sol#L539
## Tool used

Manual Review

## Recommendation
```solidity
function increaseLiquidity(uint256 positionId, uint128 amount) external virtual nonReentrant {
    (assetState_, positionState_) = _getRewardBalances(assetState_, positionState_);

    assetState_.totalStaked = assetState_.totalStaked + amount;

+   assetState_.lastRewardGlobal = 0;

    positionState[positionId] = positionState_;
    assetState[asset] = assetState_;
    _stake(asset, amount);
}
```