Salty Lead Fox

high

# Happy flow and early termination won't trigger in practice due to ever-decreasing price of auctioned assets

## Summary
Liquidations cannot realistically be settled through early termination or the happy flow. This is because the price of the auctioned assets is ever-decreasing, and full payout of the debt won't occur.

## Vulnerability Detail
It's impossible in practice to settle an auction through the happy flow or early termination. Liquidations are started by calling `Liquidator::liquidateAccount()`. The function caches auction parameters like `startTime`, `cutOffTime`, and `startDebt` into the `auctionInformation` mapping and initiates the auction. Then, anyone can bid through `Liquidator::bid()` for the assets of the account. The value of those decreases exponentially over time and is calculated with the following formula: `P(t) = Debt * S * [(SPM - MPM) * base^t + MPM]`.

The problem here is that to trigger an early termination or happy flow, the whole initial debt of the account has to be covered. This is practically impossible since the price the auction asks for the account's assets is calculated in a different way than the account's collateral value and debt and also decreases over time.

Here's the code for `Liquidation::bid()`:

```solidity
function bid(address account, uint256[] memory askedAssetAmounts, bool endAuction_) external nonReentrant {
    AuctionInformation storage auctionInformation_ = auctionInformation[account];
    if (!auctionInformation_.inAuction) revert LiquidatorErrors.NotForSale();

    // Calculate the current auction price of the assets being bought.
    uint256 totalShare = _calculateTotalShare(auctionInformation_, askedAssetAmounts);
    uint256 price = _calculateBidPrice(auctionInformation_, totalShare);

    // Transfer an amount of "price" in "Numeraire" to the LendingPool to repay the Accounts debt.
    // The LendingPool will call a "transferFrom" from the bidder to the pool -> the bidder must approve the LendingPool.
    // If the amount transferred would exceed the debt, the surplus is paid out to the Account Owner and earlyTerminate is True.
    uint128 startDebt = auctionInformation_.startDebt;
    bool earlyTerminate = ILendingPool(auctionInformation_.creditor).auctionRepay(
        startDebt, auctionInformation_.minimumMargin, price, account, msg.sender
    );

    // Transfer the assets to the bidder.
    IAccount(account).auctionBid(
        auctionInformation_.assetAddresses, auctionInformation_.assetIds, askedAssetAmounts, msg.sender
    );

    // If all the debt is repaid, the auction must be ended, even if the bidder did not set endAuction to true.
    if (earlyTerminate) {
        // Stop the auction, no need to do a health check for the account since it has no debt anymore.
        _endAuction(account);
    }
    // If not all debt is repaid, the bidder can still earn a termination incentive by ending the auction
    // if one of the conditions to end the auction is met.
    // "_endAuction()" will silently fail without reverting, if the auction was not successfully ended.
    else if (endAuction_) {
        if (_settleAuction(account, auctionInformation_)) _endAuction(account);
    }
}
```

This calls into `LiquidationPool::auctionRepay()`, which is the one responsible for triggering early termination and the happy flow:

```solidity
function auctionRepay(uint256 startDebt, uint256 minimumMargin_, uint256 amount, address account, address bidder)
    external
    whenLiquidationNotPaused
    onlyLiquidator
    processInterests
    returns (bool earlyTerminate)
{
    // Need to transfer before burning debt or ERC777s could reenter.
    // Address(this) is trusted -> no risk on re-entrancy attack after transfer.
    asset.safeTransferFrom(bidder, address(this), amount);

    uint256 accountDebt = maxWithdraw(account);
    if (accountDebt == 0) revert LendingPoolErrors.IsNotAnAccountWithDebt();
    if (accountDebt <= amount) {
        // The amount recovered by selling assets during the auction is bigger than the total debt of the Account.
        // -> Terminate the auction and make the surplus available to the Account-Owner.
        earlyTerminate = true;
        unchecked {
            _settleLiquidationHappyFlow(account, startDebt, minimumMargin_, bidder, (amount - accountDebt));
        }
        amount = accountDebt;
    }

    _withdraw(amount, address(this), account);

    emit Repay(account, bidder, amount);
}
```

What we can see here is that if the price derived from `_calculateBidPrice(auctionInformation_, totalShare);` is equal to or greater than `accountDebt`, the auction is settled in the happy flow. This is impossible in reality because the price is constantly decreasing. Even if we assume it was possible, it wouldn't be practical since the terminator (last bidder) would only be getting the termination rewards out of this settlement. There's an additional problem here. Even if we assumed auctions can be settled in the case `accountDebt == amount`, they cannot be settled in the case `accountDebt <= amount`. If `amount` is larger, the code currently passes the difference into the `surplus` param of `_settleLiquidationHappyFlow()` and sends the difference to the account owner. It can be observed inside of the function:

```solidity
totalRealisedLiquidity =
    SafeCastLib.safeCastTo128(totalRealisedLiquidity + terminationReward + liquidationPenalty + surplus);

unchecked {
    // Pay out any surplus to the current Account Owner.
    if (surplus > 0) realisedLiquidityOf[IAccount(account).owner()] += surplus;
    // Pay out the "terminationReward" to the "terminator".
    realisedLiquidityOf[terminator] += terminationReward;
}

_endLiquidation();
```

The problem here is that once this gets popped off the execution stack, the code will return to the following line inside `LendingPool::auctionRepay()`:

```solidity
_withdraw(amount, address(this), account);
```

This will revert with an underflow in all cases since it's trying to burn debt tokens that are larger than the account's current balance.

Now, if we go back to `Liquidation.bid()`, we will see the following line at the end:

```solidity
else if (endAuction_) {
  if (_settleAuction(account, auctionInformation_)) _endAuction(account);
}
```

This triggers when `endAuction_` of `true` is passed as an argument. `_settleAuction()` is supposed to also be able to trigger the happy flow under a specific condition. Here's the part we're interested in:

```solidity
// Cache variables.
uint256 startDebt = auctionInformation_.startDebt;
address creditor = auctionInformation_.creditor;
uint96 minimumMargin = auctionInformation_.minimumMargin;

uint256 collateralValue = IAccount(account).getCollateralValue();
uint256 usedMargin = IAccount(account).getUsedMargin();

// Check the different conditions to end the auction.
if (collateralValue >= usedMargin || usedMargin == minimumMargin) {
    // Happy flow: Account is back in a healthy state.
    // An Account is healthy if the collateral value is equal or greater than the used margin.
    // If usedMargin is equal to minimumMargin, the open liabilities are 0 and the Account is always healthy.
    ILendingPool(creditor).settleLiquidationHappyFlow(account, startDebt, minimumMargin, msg.sender);
}

// ...
```

In practice, this is also a case that will never trigger. The collateral value is only decreasing as people bid and take it, the used margin is also proportionally decreasing but they will never align since the account's debt is always larger for the reasons I described above. `usedMargin == minimumMargin` is an unrealistic case for the same reason.

With this, auctions will only end through the unhappy flow, either when the collateral value drops to 0 or the cut-off time passes.

```solidity
else if (collateralValue == 0) {
    // Unhappy flow: All collateral is sold.
    ILendingPool(creditor).settleLiquidationUnhappyFlow(account, startDebt, minimumMargin, msg.sender);
} else if (block.timestamp > auctionInformation_.cutoffTimeStamp) {
    // Unhappy flow: Auction did not end within the cutoffTime.
    ILendingPool(creditor).settleLiquidationUnhappyFlow(account, startDebt, minimumMargin, msg.sender);
    // All remaining assets are transferred to the asset recipient,
    // and a manual (trusted) liquidation has to be done.
    IAccount(account).auctionBoughtIn(creditorToAccountRecipient[creditor]);
}
```

In the unhappy flow, losses are absorbed by the trenches, which in turn leads to losses for the LPs and the protocol.
<details>
<summary><h3>Coded POCs (use test/fuzz/Liquidator/Bid.fuzz.t.sol)</h3></summary>

```solidity
function testCannotEndAuctionWithEarlyTermination() public {
    // Create Bidder
    address bidder = makeAddr("bidder");

    // Auction gets initiated
    uint112 amountLoaned = 100_000;
    initiateLiquidation(amountLoaned);

    // Let some time pass so price depreciates
    vm.warp(block.timestamp + 1.5 hours);

    // Cache initial debt
    uint256 initialDebt = pool.maxWithdraw(address(proxyAccount));

    // Configure parameters
    bool endAuction = false;
    uint256[] memory originalAssetAmounts = liquidator.getAuctionAssetAmounts(address(proxyAccount));
    uint256 originalAmount = originalAssetAmounts[0];
    uint256[] memory bidAssetAmounts = new uint256[](1);
    uint256 bidAssetAmount = originalAmount;
    bidAssetAmounts[0] = bidAssetAmount;
    // Price is based on account's full original collateral
    uint256 askedShare = liquidator.calculateTotalShare(address(proxyAccount), bidAssetAmounts);
    uint256 askPrice_ = liquidator.calculateBidPrice(address(proxyAccount), askedShare);
    assert(amountLoaned > askPrice_);

    // Mint bidder enough funds to bid for full collateral
    deal(address(mockERC20.stable1), bidder, askPrice_);

    // bid for all of account's assets
    vm.startPrank(bidder);
    mockERC20.stable1.approve(address(pool), bidAssetAmount);
    liquidator.bid(address(proxyAccount), bidAssetAmounts, endAuction);
    vm.stopPrank();

    uint256 expectedUsedMargin = initialDebt - askPrice_;

    // Bidder has succesfully snipped accounts' assets
    assert(mockERC20.stable1.balanceOf(bidder) == originalAmount);
    // no collateral inside account, it's all taken
    assert(proxyAccount.getCollateralValue() == 0);
    // proves usedMargin is greater than collateral
    assert(proxyAccount.getUsedMargin() == expectedUsedMargin);
    // still in auction and with debt since it wasn't ended
    assert(pool.maxWithdraw(address(proxyAccount)) != 0);
}

function testCannotEndAuctionInHappyFlowWithParamTrue() public {
    // Create Bidder
    address bidder = makeAddr("bidder");

    // Auction gets initiated
    uint112 amountLoaned = 100_000;
    initiateLiquidation(amountLoaned);

    // Let some time pass so price depreciates
    vm.warp(block.timestamp + 1.5 hours);

    // Configure parameters
    bool endAuction = true;
    uint256[] memory originalAssetAmounts = liquidator.getAuctionAssetAmounts(address(proxyAccount));
    uint256 originalAmount = originalAssetAmounts[0];
    uint256[] memory bidAssetAmounts = new uint256[](1);
    uint256 bidAssetAmount = originalAmount;
    bidAssetAmounts[0] = bidAssetAmount;
    // Price is based on account's full original collateral
    uint256 askedShare = liquidator.calculateTotalShare(address(proxyAccount), bidAssetAmounts);
    uint256 askPrice_ = liquidator.calculateBidPrice(address(proxyAccount), askedShare);

    // assert that the amountLoaned is greater than the asked price, this ensures that loan is not settled with incoming bid
    assert(amountLoaned > askPrice_);

    // Mint bidder enough funds to bid for full collateral
    deal(address(mockERC20.stable1), bidder, askPrice_);

    // bid for all of collateral's assets
    vm.startPrank(bidder);
    mockERC20.stable1.approve(address(pool), bidAssetAmount);
    liquidator.bid(address(proxyAccount), bidAssetAmounts, endAuction);
    vm.stopPrank();

    // Bidder has succesfully snipped accounts' assets
    assert(mockERC20.stable1.balanceOf(bidder) == originalAmount);
    // no collateral inside account, it's all taken
    assert(proxyAccount.getCollateralValue() == 0);
    // no outstanding debt, auction was settled through unhappy flow, which resets the debt
    assert(pool.maxWithdraw(address(proxyAccount)) == 0);
}
```
</details>

## Impact
Core features of the auction such as early termination and the happy flow are, in practice, impossible to trigger and will cause all auctions to be settled in the unhappy flow eventually. The unhappy flow leads to losses for the LPs and protocol as the tranches handle the loss absorption there.

## Code Snippet
https://github.com/sherlock-audit/2023-12-arcadia/blob/main/lending-v2/src/Liquidator.sol#L285-L317
https://github.com/sherlock-audit/2023-12-arcadia/blob/main/lending-v2/src/LendingPool.sol#L498-L510
https://github.com/sherlock-audit/2023-12-arcadia/blob/main/lending-v2/src/Liquidator.sol#L301-L316
https://github.com/sherlock-audit/2023-12-arcadia/blob/main/lending-v2/src/Liquidator.sol#L439-L455

## Tool used

Manual Review

## Recommendation
There is no simple fix here since there are multiple wrong assumptions that lead to the issue. The happy flow and early termination features should be carefully reviewed and revised by the protocol team.